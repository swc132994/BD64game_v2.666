ACTOR 64Carrion 19136
{
//$category "Game/BD64_Monsters
Scale 0.7
Health 50
PainChance 100
Speed 6
Radius 16
Height 52
Mass 400
MaxTargetRange 250
DamageFactor "CauseWaterSplash",0.0
DamageFactor "CauseObjectsToSplash",0.0
DamageFactor "chainsaw",0.4
SeeSound "Carrion/SCREAMING"
ActiveSound "Carrion/Active"
DeathSound "Carrion/Detonate"
PainSound "Carrion/SCREAMING"
AttackSound "Carrion/Active"
DropItem "SOULSPAWNER"
Tag "Carrion Zombie"
Obituary "The Horde infected %o."
MONSTER
+FLOORCLIP
+NOBLOODDECALS
+LOOKALLAROUND
-MISSILEMORE
-MISSILEEVENMORE
-COUNTKILL
	States
	{
	Spawn:
		CRRI E 10 A_LookEx
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget10",0,0,56)
		Loop
	Idle:
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget2",0,0,56)
		CRRI E 2 A_ClearTarget
		Goto Spawn
	See:
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget4",7,0,56)
		CRRI AA 2 A_Chase
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget4",7,0,56)
		CRRI BB 2 A_Chase
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget4",7,0,56)
		CRRI CC 2 A_Chase
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget4",7,0,56)
		CRRI DD 2 A_Chase
		TNT1 A 0 A_JumpIfInTargetInventory("PowerInvisibility",1,"Idle")
		Loop
	Missile:
		TNT1 A 0 A_JumpIfCloser(90,"Melee")
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget2",7,0,56)
		CRRI AAA 1 A_Chase
		TNT1 A 0 A_JumpIfCloser(90,"Melee")
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget2",7,0,56)
		CRRI BBB 1 A_Chase
		TNT1 A 0 A_JumpIfCloser(90,"Melee")
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget2",7,0,56)
		CRRI CCC 1 A_Chase
		TNT1 A 0 A_JumpIfCloser(90,"Melee")
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget2",7,0,56)
		CRRI DDD 1 A_Chase
		Goto See
	Melee:
		TNT1 A 0 A_PlaySound("Carrion/Detonate",CHAN_AUTO)
		TNT1 A 0 A_SpawnItemEx("BarrelExplosionSmokeColumn")
		CRRI F 2 Bright Light("TINYREDTORCH") A_FaceTarget
		CRRI F 2 Bright Light("TINYREDTORCH") A_Die("Detonated")
		Goto Death
	Pain:
		CRRI E 2 A_Pain
		Goto See
	Death:
		CRRI E 1
		Goto ContinueDeath
	Death.Detonated:
		TNT1 A 0 A_SpawnItemEx("BarrelExplosionSmokeColumn",random(-10,10),random(-10,10),20)
		TNT1 A 0 A_SpawnItemEx("BarrelExplosionSmokeColumn")
		CRRI F 1 Bright Light("TINYREDTORCH") A_AlertMonsters
		TNT1 A 0 A_Explode(50,150)
		TNT1 AAAAAAAA 0 A_SpawnItemEx("64PoisonBloodProjectileD",0,0,48,random(-6,6),random(-6,6),random(4,6),random(0,359),0)
	ContinueDeath:
		TNT1 AAAAAA 0 A_CustomMissile("CeilBloodLauncher",0,0,random(0,360),2,random(50,130))
		TNT1 AA 0 A_CustomMissile("XDeath1",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath3",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeath5",32,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAAA 0 A_CustomMissile("SmallBrainPiece",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XdeathTeeth",56,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XdeathEyes",40,0,random(0,360),2,random(-5,5))
		TNT1 A 0 A_CustomMissile("XDeathOrgan1",40,0,random(0,360),2,random(-5,5))
		TNT1 A 0 A_CustomMissile("XDeathOrgan2",40,0,random(0,360),2,random(-5,5))
		TNT1 AAAA 0 A_CustomMissile("Intestine",32,0,random(0,360),2,random(0,90))
		TNT1 AAAA 0 A_CustomMissile("Bloodmist",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAA 0 A_CustomMissile("BloodmistBig",45,0,random(0,360),2,random(10,90))
		CRRI G 4
		CRRI H 4 A_XScream
		CRRI I 2
		CRRI J 2 A_NoBlocking
		CRRI KLMNO 2
		CRRI O 35
		CRRI P -1 A_CustomMissile("GrowingBloodPool",0,0,random(0,360),2,random(0,90))
		Stop
	Crush:
		TNT1 A 0 A_PlaySound("Misc/Gibbed")
		POL5 A -1
		Stop
	}
}