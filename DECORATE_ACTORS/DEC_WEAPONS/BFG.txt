Actor BFG9000Spawner : CustomInventory Replaces BFG9000
{
//$category "Game/BD64_Weapons
Scale 0.9
Inventory.PickupMessage "$GOTBFG9000"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		BFUG A 2
		TNT1 A 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		BFUG A 2
		TNT1 A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		BFUG A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64BFG9000")
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64BFG9000Tactical")
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",1)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64BFG9000")
		TNT1 A 0 A_SpawnItemEx("64BFG9000TACTICAL")
		TNT1 A 1050
		Loop
	}
}

Actor 64BFG9000 : DoomWeapon
{
Scale 0.9
Weapon.SlotNumber 7
Weapon.SelectionOrder 2800
Weapon.AmmoType "Cell"
Weapon.AmmoUse 40
Weapon.AmmoGive 40
Inventory.PickupMessage "$GOTBFG9000"
Tag "$TAG_BFG9000"
+WEAPON.NOAUTOFIRE
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Ready:
		TNT1 A 0 A_StopSound(5)
		BFGG A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_StopSound(5)
		BFGG A 1 A_Lower
		Wait
	Select:
		BFGG A 1 A_Raise
		Wait
	Fire:
		BFGG A 1 A_BFGSound
		BFGC ABCDABCDABCDABCDABCDABCD 1 Bright
		TNT1 A 0 A_FireBFG
		TNT1 A 0 A_ZoomFactor(0.9)
		TNT1 A 0 A_SetPitch(pitch-4, SPF_INTERPOLATE)
		BFGF AB 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		BFGF C 1 Bright
		BFGF GGGHIJ 1 A_SetPitch(pitch+0.5, SPF_INTERPOLATE)
		BFGG A 20
		BFGG A 1 A_ReFire
		Goto Ready
	Spawn:
		BFUG A -1
		Stop
	}
}

Actor 64BFG9000Tactical : Brutal64Weapon
{
Scale 0.9
Weapon.SlotNumber 7
Weapon.SelectionOrder 2800
Weapon.AmmoType "Cell"
Weapon.AmmoUse 1
Weapon.AmmoGive 40
Inventory.PickupMessage "$GOTBFG9000"
Tag "$TAG_BFG9000"
+WEAPON.NOAUTOFIRE
+WEAPON.ALT_USES_BOTH
+WEAPON.NOALERT
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	MineThrowFlash:
		BFGZ ABCD 1
		TNT1 D 18
		BFGZ DCBA 1
		Stop
	KickingFlash:
		BFGZ ABCD 1
		BFGZ D 8
		BFGZ DCBA 1
		Stop
	AirKickingFlash:
		BFGZ ABCD 1
		BFGZ D 10
		BFGZ DCBA 1
		Stop
	SlideKickingStart:
		BFGZ ABCD 1
		BFGZ D 18
	SlideKickingEnd:
		BFGZ D 6
		BFGZ DCBA 1
		Stop
	v1Ready:
	Ready:
		TNT1 A 0 A_StopSound(5)
		BFGG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_StopSound(5)
		BFGS EDCBA 1
		TNT1 A 1 A_Lower
		Wait
	Select:
		TNT1 A 0 A_PlaySound("BFGREADY")
		BFGS ABCDE 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("CELL",40,2)
		TNT1 A 0 A_Print("Not Enough Ammo!",1)
		Goto Ready
		TNT1 A 0 A_AlertMonsters
		BFGG A 1 A_BFGSound
		BFGC ABCDABCDABCDABCDABCDABCD 1 Bright
		TNT1 A 0 A_FireBFG
		TNT1 A 0 A_TakeInventory("CELL",39,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_ZoomFactor(0.9)
		TNT1 A 0 A_SetPitch(pitch-4, SPF_INTERPOLATE)
		BFGF AB 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		BFGF C 1 Bright
		BFGF GGGHIJ 1 A_SetPitch(pitch+0.5, SPF_INTERPOLATE)
		BFGG A 17
		BFGG A 1 A_ReFire
		Goto Ready
	Spawn:
		BFUG A -1
		Stop
	AltFire:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,2)
		TNT1 A 0 A_JumpIfInventory("IsModern",1,1)
		Goto Ready
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Playsound("weapons/bfgf2",CHAN_WEAPON)
		BFGC ABCDAFCDEFCHABGDAFCDH 1 Bright
	AltHold:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("BBFGHOLD",5,1,1)
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall2",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_Jump(128,3)
		BFGF A 1 Bright
		TNT1 A 0 A_Jump(255, 2)
		BFGF D 1 Bright
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall2",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_Jump(128 ,3)
		BFGF B 1 Bright
		TNT1 A 0 A_Jump(255, 2)
		BFGF E 1 Bright
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall2",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_Jump(128 ,3)
		BFGF C 1 Bright
		TNT1 A 0 A_Jump(255, 2)
		BFGF F 1 Bright
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_FireCustomMissile("BFGBetaBall2",random(8,-8),1,random(5,-5),random(5,-5),0,random(5,-5))
		TNT1 A 0 A_SetPitch(pitch-2, SPF_INTERPOLATE)
		TNT1 A 0 A_ZoomFactor(0.9)
		BFGF JI 1 
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_ReFire
		BFGF GGGHIJ 1 A_SetPitch(pitch+0.5, SPF_INTERPOLATE)
		TNT1 A 0 A_StopSound(5) 
		Goto Ready
	}
}

Actor 64BFGBall : BFGBall Replaces BFGBall
{
Scale 0.75
Radius 8
Height 8
Renderstyle Normal
DamageType "BFG"
Decal "BFGLIGHTNING"
+EXTREMEDEATH
	states
	{
	Spawn:
		TNT1 A 0 A_SpawnItemEx("BFGFlare")
		BFS1 AABB 1 Bright A_SpawnItemEx("BFGFlare")
		Loop
	Death:
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleGreenHeavy",0,0,random(0,360),2,random(10,70))
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreenHeavySmall",0,0,random(0,360),2,random(20,50))
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreenVeryFast",0,0,random(0,360),2,random(20,40))
		TNT1 AAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreen",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAA 0 A_CustomMissile("64BFGFireBase",0,0,random(0,360),2,random(0,360))
		TNT1 AAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater")
		TNT1 A 0 A_SpawnItemEx("ExplosionAirDisplacementEffect")
		TNT1 A 0 Radius_Quake(4,8,0,12,0)
		TNT1 A 0 A_Explode(200,180,0)
		TNT1 A 2
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		BFE1 A 6 Bright
		BFE1 B 7 Bright A_FadeOut(0.142857)
		BFE1 C 1 Bright A_FadeOut(0.142857)
		BFE1 C 3 Bright A_BFGSpray
		BFE1 D 1 Bright A_FadeOut(0.142857)
		BFE1 E 1 Bright A_FadeOut(0.142857)
		BFE1 F 1 Bright A_FadeOut(0.142857)
		Stop
	}
}

Actor 64BFGFireBase
{
Scale 1.2
Speed 6
Radius 1
Height 1
Alpha 0.8
RenderStyle Add
BounceFactor 0.5
+DOOMBOUNCE
+NOBLOCKMAP
+NOTELEPORT
+DONTSPLASH
+MISSILE
+FORCEXYBILLBOARD
+CLIENTSIDEONLY
+NOINTERACTION
+NOGRAVITY
+THRUACTORS
	States
	{
	Spawn:
		BFE1 ABCDEF 4 Bright
		Stop
	}
}

Actor 64BFGExtra : BFGExtra Replaces BFGExtra
{
Renderstyle Normal
DamageType "BFG"
+EXTREMEDEATH
	states
	{
	Spawn:
		BFE2 ABCDEF 2 Bright
		Stop
	}
}

Actor BFGBetaBall : PlasmaBall
{
Scale 0.75
Damage 10
DamageType Extreme
Obituary "%o was obliterated by %k's BFG."
SeeSound ""
Decal MedScorch
+BOUNCEONFLOORS
+BOUNCEONCEILINGS
BounceFActor 0.95
ReactionTime 60
	States
	{
	Spawn:
		GRNB ACB 2 Bright A_CHECKCEILING("Death")
		TNT1 A 0 A_Countdown
		Loop
	Death:
		TNT1 A 0 A_PlaySound("DSFIRXPL",3)
		BFE2 ABCD 3 Bright
		TNT1 AAAAA 0 A_CustomMissile("explosionParticleGreenHeavySmall",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreen",0,0,random(0,360),2,random(0,360))
		TNT1 A 1 A_Explode(100,70)
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterMed")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterMed")
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		TNT1 A 9 A_CustomMissile("PlasmaSmoke",1,0,random(0,360),2,random(0,160))
		Stop
	}
}

Actor BFGBetaBall2 : BFGBetaBall
{
	States
	{
	Spawn:
		LAZR AB 2 Bright
		TNT1 A 0 A_Countdown
		Loop
	Death:
		TNT1 A 0 A_PlaySound("DSFIRXPL",3)
		REDB ABCD 3 Bright
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("ExplosionParticleLaser",0,0,random(0,360),2,random(0,360))
		TNT1 A 1 A_Explode(100,70)
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterMed")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterMed")
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		TNT1 A 9 A_CustomMissile("PlasmaSmoke",1,0,random(0,360),2,random(0,160))
		Stop
	}
}