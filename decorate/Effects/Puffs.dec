ACTOR HitPuff Replaces BulletPuff
{
Scale 1.5
radius 0
height 0
Mass 500
Gravity 0.01
alpha 0.4
renderstyle Add
DamageType "Bullet"
Decal "BulletDecalNew1"
+NOBLOCKMAP
+NOGRAVITY
+NOEXTREMEDEATH
+FORCEXYBILLBOARD
	states
	{
	Spawn:
		PUFF A 2 BRIGHT
		PUFF B 2 BRIGHT A_SpawnItemEx("RicoChet",0,0,-5)
		//Intentional fall-through
	Melee:
		PUFF CDEF 2
		Stop
	}
}

ACTOR HitPuffSilence : HitPuff
{+FORCEPAIN}

Actor UnderwaterHitPuff
{
+NOCLIP
+NOGRAVITY
	states
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_JumpIf(waterlevel < 1,"Stap")
		TNT1 A 0 A_CustomMissile("WaterParticleXTinySpawner",0,0,random(0,360),2,random(60,130))
		TNT1 A 0 A_CustomMissile("BUBULZ",0,0,random(0,360),2,random(0,180))
		TNT1 A 1 A_CustomMissile("EXPLOSIONBUBULZ",0,0,random(0,360),2,random(180,360))
		Stop
	Stap:
		TNT1 A 3
		Stop
	}
}

actor ShotPuff : HitPuff
{
DamageType "SSG"
}

actor ShotgunPuff : HitPuff
{
DamageType "Shotgun"
}

ACTOR RicoChet
{
Scale 0.09
Speed 0
Radius 1
Height 1
alpha 1.0
renderstyle Add
Obituary "%o ate bullet shrapnel."
+CLIENTSIDEONLY
+BLOODLESSIMPACT
+FORCEXYBILLBOARD
+THRUACTORS
+NOGRAVITY
	states
	{
	Spawn:
		IPF2 A 0 NoDelay A_SpawnItemEx("YellowFlareSmall")
		TNT1 A 0 A_SpawnItemEx("DetectFloorBullet")
		TNT1 A 0 A_SpawnItemEx("DetectCeilBullet")
		TNT1 A 0 A_SpawnItemEx("UnderwaterHitPuff")
		TNT1 AAAA 0 A_CustomMissile("HitSpark",2,0,random(0,360),2,random(30,360))
		TNT1 AA 0 A_CustomMissile("HitSpark22",2,0,random(0,360),2,random(30,360))
		TNT1 A 0 A_CustomMissile("HitSpark23",2,0,random(0,360),2,random(30,360))
		TNT1 A 0 A_PlaySound("ricochet/hit",CHAN_BODY)
		TNT1 AAA 0 A_CustomMissile("OldschoolRocketSmokeTrail2",2,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_Jump(256,"Spawn1","Spawn2","Spawn3","Spawn4")
	Spawn1:
		IPF2 AABB 1 Bright A_SpawnItemEx("YellowFlareSmall")
		Stop
	Spawn2:
		IPF2 CCDD 1 Bright A_SpawnItemEx("YellowFlareSmall")
		Stop
	Spawn3:
		IPF2 EEFF 1 Bright A_SpawnItemEx("YellowFlareSmall")
		Stop
	Spawn4:
		IPF2 GGHH 1 Bright A_SpawnItemEx("YellowFlareSmall")
		Stop
	}
}

Actor MachineGunBulletPuff : HitPuff
{
DamageType "CutLess"
}

Actor RifleBulletPuff : HitPuff
{
DamageType "Rifle"
}

ACTOR SSawPuff : HitPuff
{
scale 0.15
Radius 20
alpha 0.7
Renderstyle Add
DamageType "Cut"
+NOBLOCKMAP
+NOGRAVITY
+NOEXTREMEDEATH
+FORCEXYBILLBOARD
	states
	{
	Spawn:
		BPUF C 1 Bright
	Death:
	XDeath:
	Melee:
		TNT1 A 2
		TNT1 AAAA 0 A_CustomMissile("HitSpark",2,0,random(0,360),2,random(30,360))
		TNT1 AA 0 A_CustomMissile("HitSpark22",2,0,random(0,360),2,random(30,360))
		TNT1 A 0 A_CustomMissile("HitSpark23",2,0,random(0,360),2,random(30,360))
		BPUF CD 1 Bright
		Stop
	}
}

Actor Tracer : FastProjectile
{
scale 0.15
radius 2
height 2
speed 140
alpha 0.5
renderstyle ADD
+FORCEXYBILLBOARD
+DONTSPLASH
+NOEXTREMEDEATH
	states
	{
	Spawn:
		SPKO S -1 Bright
		Stop
	XDeath:
	Death:
		TNT1 A 3
		Stop
	}
}