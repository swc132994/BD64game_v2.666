Actor AltRifleSkin : Inventory
{
	Inventory.MaxAmount 1
}

Actor RifleSpawner : CustomInventory 19120
{
//$category "Game/BD64_Weapons
Scale 0.82
Tag "Assault Rifle"
Inventory.PickupMessage "You got an assault rifle!"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		ARSD A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFD A 0
		"####" A 2
		"####" A 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		"####" A 2
		"####" A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		"####" A 1
		Loop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64Rifle",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64RifleTactical",1)
		Stop
	SpawnChaingun:
		TNT1 A 1 A_SpawnItemEx("ChaingunSpawner",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERTID,0,tid)
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64Rifle")
		TNT1 A 0 A_SpawnItemEx("64RifleTactical")
		TNT1 A 1050
		Loop
	}
}

Actor Zoomed : Inventory
{
inventory.maxamount 1
}

Actor 64Rifle : DoomWeapon
{
Scale 0.82
Weapon.SlotNumber 4
Weapon.SlotPriority 1
Weapon.SelectionOrder 800
Weapon.AmmoType "Clip"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
Inventory.PickupMessage "You got the assault rifle!"
Obituary "%o was picked off by %k."
Tag "Assault Rifle"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Spawn:
		ARSD A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFD A 0
		"####" A 1
		Loop
	Ready:
		ARSG A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFG A 0
		"####" A 1 A_WeaponReady
		Loop
	Select:
		ARSG A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFG A 0
		"####" A 1 A_Raise
		Wait
	Deselect:
		ARSG A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFG A 0
		"####" A 1 A_Lower
		Wait
	Fire:
		ARSF A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFF A 0
		"####" A 1 Bright A_FireBullets(0.0,0.0,-1,7,"RifleBulletPuff")
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_PlaySound("RIFLE",CHAN_WEAPON)
		ARSF A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFF A 0
		"####" BC 1
		"####" A 0 A_Firecustommissile("50CaseSpawn",0,0,2,-16)
		ARSG A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFG A 0
		"####" A 1
		"####" A 0 A_Refire
		"####" A 1
		Goto Ready
	}
}

Actor RifleAmmo : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 31
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 31
Inventory.Icon "RIFDA0"
+IGNORESKILL
}

Actor RifleBolt : Inventory
{
inventory.maxamount 1
}

Actor NeverSelectedRifle : Inventory
{
inventory.maxamount 1
}

Actor 64RifleTactical : Brutal64Weapon
{
Scale 0.82
Weapon.SlotNumber 4
Weapon.SlotPriority 1
Weapon.SelectionOrder 800
Weapon.AmmoType1 "64HighCaliberBox"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 20
Weapon.AmmoType2 "RifleAmmo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.PickupMessage "You got the assault rifle!"
Obituary "%o was picked off by %k Assualt Rifle."
Tag "Assault Rifle"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.AMMO_CHECKBOTH
+WEAPON.NOALERT
+WEAPON.NOAUTOFIRE
	States
	{
	MineThrowFlash:
		ARSS D 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFS D 0
		"####" DCBA 1
		TNT1 A 18
		ARSS A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFS A 0
		"####" ABCD 1
		Stop
	KickingFlash:
		ARSK A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFK A 0
		"####" BCDE 1
		"####" E 8
		"####" EDCB 1
		Stop
	SlideKickingStart:
		ARSK A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFK A 0
		"####" BCDE 1
		"####" E 35
	SlideKickingEnd:
		"####" E 2
		"####" EDCB 1
		Stop
	Spawn:
		ARSD A 0 NoDelay A_JumpIfInventory("AltRifleSkin",1,2)
		RIFD A 0
		"####" A 1
		Loop
	v1Ready:
	Ready:
		ARSG A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFG A 0
		"####" A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	NoAmmo:
		ARSG A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFG A 0
		"####" A 1
		ARSG A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFG A 0
		"####" A 10 A_PlaySound("DRYFIRE",CHAN_WEAPON)
		Goto Ready
	LetsGetStarted:
		TNT1 A 0 A_TakeInventory("StartWeapon")
		PUN1 A 10 A_WeaponReady
		PUNS ABC 1 A_WeaponReady
		PUNS D 7 A_PlaySound("Knuckled",CHAN_VOICE)
		PUNS EFED 3 A_WeaponReady
		PUNS I 7 A_PlaySound("Knuckled",CHAN_VOICE)
		PUNS JKJI 3 A_WeaponReady
		PUNS CBA 1 A_WeaponReady
		PUN6 DCBA 1	A_WeaponReady
	WarmUp:
		ARSS A 0 A_JumpIfInventory("AltRifleSkin",1,"AltWarmUp")
		TNT1 A 0 A_TakeInventory("NeverSelectedRifle")
		RIFW A 1 Offset(-56,53)
		RIFW A 1 Offset(-48,39)
		RIFW A 1 Offset(-38,34)
		RIFW A 1 Offset(-24,32)
		RIFW A 10 A_WeaponReady
		RIFW BCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		RIFW "[" 1 A_PlaySound("RIFLOAD",CHAN_WEAPON)
		RIFW "\]" 1
		R1FW ABCDEFGHIJKLMN 1
		R1FW O 3 A_PlaySound("RIFBOLT",CHAN_WEAPON)
		R1FW PQRSTUVWXYZ 1 A_WeaponReady
		R1FW "[\]" 1 A_WeaponReady
		R2FW ABC 1 A_WeaponReady
		Goto Ready
	AltWarmUp:
		TNT1 A 0 A_TakeInventory("NeverSelectedRifle")
		ARSR D 1 Offset(-97,56)
		ARSR D 1 Offset(-73,50)
		ARSR D 1 Offset(-46,40)
		ARSR D 1 Offset(-27,35)
		ARSR D 1 Offset(-19,32)
		ARSR EFF 1 A_WeaponReady
		ARSR GG 1 Offset(0,32)
		ARSR HIJKLMNNOPPPQ 1
		RIFL C 0 A_PlaySound("Relo2",CHAN_AUTO)
		ARSR RST 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("Relo3",CHAN_AUTO)
		ARSR U 1 A_WeaponReady
		AR2R CDEGHII 1 A_WeaponReady
		AR2R J 3 A_WeaponReady
		TNT1 A 0 A_PlaySound("ARSBOLT",CHAN_WEAPON)
		AR2R LM 1 A_WeaponReady
		AR2R N 4 A_WeaponReady
		AR2R OPQ 1 A_WeaponReady
		AR2R R 3 A_WeaponReady
		AR2R SSTUV 1 A_WeaponReady
		ARSR Z 1 A_WeaponReady
		AR2R AB 1 A_WeaponReady
		Goto Ready
	Select:
		TNT1 A 0 A_JumpIfInventory("StartWeapon",1,"LetsGetStarted")
		TNT1 A 0 A_JumpIfInventory("NeverSelectedRifle",1,"WarmUp")
		TNT1 A 0 A_PlaySound("RIFBOLT",CHAN_AUTO)
		ARSS A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFS A 0
		"####" ABCD 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Fidget:
		RIFI ABCDEFFFFFF 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("RIFBOLT",CHAN_WEAPON)
		RIFI GHIJ 1 A_WeaponReady
		RIFI K 5 A_WeaponReady
		RIFI MNOPQ 1 A_WeaponReady
		Goto Ready
	AltFidget:
		TNT1 A 0 A_Jump(69,"AltFidget2")
		ARSG BCDE 1 A_WeaponReady
		ARSG F 4 A_WeaponReady
		TNT1 A 0 A_PlaySound("ARSBOLT",CHAN_WEAPON)
		ARSG GHIIJKK 1 A_WeaponReady
		ARSG JIHGFE 1 A_WeaponReady
		ARSG DCB 1 A_WeaponReady
		Goto Ready
	AltFidget2:
		AR2R BA 1
		ARSR ZYX 1
		AR2R CDEGHII 1
		AR2R J 3
		TNT1 A 0 A_PlaySound("ARSBOLT",CHAN_WEAPON)
		AR2R LM 1
		AR2R N 4
		AR2R OPQ 1
		AR2R R 3
		AR2R SSTUV 1
		ARSR Z 1
		AR2R AB 1
		Goto Ready
	AltADSFidget:
		AR2G QRST 1
		AR2G U 3
		TNT1 A 0 A_PlaySound("ARSBOLT",CHAN_WEAPON)
		AR2G VWXYZ 1
		AR2G Z 4
		AR2G XYWV 1
		AR2G U 3
		AR2G TSRQ 1
		Goto Ready2
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_Takeinventory("Zoomed",0)
		ARSS E 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFS E 0
		"####" EFGH 1
		TNT1 A 0 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_Takeinventory("RifleAmmo",1)
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",9,2)
		TNT1 A 0 ACS_NamedExecute("LowAmmoSFX",0,0,0,0)
		TNT1 AAA 0		
		Goto ResumeFire
	NoReload:
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto NoAmmo
		TNT1 A 0 A_Takeinventory("64HighCaliberBox",1,TIF_NOTAKEINFINITE)
	ResumeFire:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"FireZoomed")
		TNT1 A 0 A_SetPitch(pitch-1.2)
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("RIFLE",CHAN_WEAPON)
		ARSF A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFF A 0
		"####" A 1 Bright A_FireBullets(0.0,0.0,1,7,"RifleBulletPuff",FBF_NORANDOM)
		"####" BC 1 A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,2,-16)
		ARSG A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFG A 0
		"####" A 1 A_SetAngle(angle + random(-1,1))
		TNT1 A 0 A_Refire
		ARSG A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFG A 0
		"####" A 1
		Goto Ready
	Reload:
		ARSG A 0 A_JumpIfInventory("AltRifleSkin",1,"AlternativeReload")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ADSReload")
		RIFG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",31,"Fidget")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto NoAmmo
		TNT1 A 0 A_PlaySound("Reload",CHAN_AUTO)
		RIFR ABCDEFGH 1 A_JumpIfInventory("Kicking",1,"DoKick")
		RIFR I 1 A_FireCustomMissile("EmptyClipSpawn",-5,0,8,-18)
		RIFR JK 1
		RIFR L 11 A_JumpIfInventory("Kicking",1,"DoKick")
		RIFR MNOPQRS 1
		RIFR TUV 2
		RIFR WXYZ 1
		R1FR ABC 1 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,"InsertBullets2")//30+1 effect
		Goto InsertBullets
	ADSReload:
		ARSG A 0 A_JumpIfInventory("AltRifleSkin",1,"ADSAlternativeReload")
		RI2G A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",31,"Ready2")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto Ready2
		TNT1 A 0 A_PlaySound("Reload",CHAN_AUTO)
		RI2R ABCDE 1 A_JumpIfInventory("Kicking",1,"DoKick")
		RI2R F 1 A_FireCustomMissile("EmptyClipSpawn",-5,0,8,-18)
		RI2R G 11 A_JumpIfInventory("Kicking",1,"DoKick")
		RI2R G 1 Offset(0,33)
		RI2R G 1 Offset(0,34)
		RI2R G 1 Offset(0,35)
		RI2R G 1 Offset(0,34)
		RI2R G 1 Offset(0,33)
		RI2R GGGGGFEDCBA 1 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,"ADSInsertBullets2")//30+1 effect
		Goto ADSInsertBullets
	AlternativeReload:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ADSAlternativeReload")
		ARSG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",31,"AltFidget")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto NoAmmo
		TNT1 A 0 A_PlaySound("Relo1",CHAN_AUTO)
		ARSR AB 1
		ARSR F 0 A_FireCustomMissile("EmptyClipSpawn",-5,0,8,-14)
		ARSR CDEFF 1
		ARSR GG 1 offset(0,32)
		ARSR HIJKLMNNOPPPQ 1
		RIFL C 0 A_PlaySound("Relo2",CHAN_AUTO)
		ARSR RST 1
		TNT1 A 0 A_PlaySound("Relo3",CHAN_AUTO)
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,3)
		TNT1 A 0 A_GiveInventory("RifleBolt",1)
		TNT1 A 0 A_JumpIfInventory("RifleBolt",1,"ReloadEmpty")
		ARSR U 4
		ARSR VXYZ 1
		AR2R AB 1
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,"InsertBullets2")//30+1 effect
	ReloadEmpty:
		TNT1 A 0 A_TakeInventory("RifleBolt",1)
		ARSR U 1
		AR2R CDEGHII 1
		AR2R J 3
		TNT1 A 0 A_PlaySound("ARSBOLT",CHAN_WEAPON)
		AR2R LM 1
		AR2R N 4
		AR2R OPQ 1
		AR2R R 3
		AR2R SSTUV 1
		ARSR Z 1
		AR2R AB 1
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",30,"Ready")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto Ready
		TNT1 A 0 A_Giveinventory("RifleAmmo",1)
		TNT1 A 0 A_Takeinventory("64HighCaliberBox",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets
		TNT1 A 0 A_Refire
		Goto Ready
	InsertBullets2:
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",31,"Ready")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto Ready
		TNT1 A 0 A_Giveinventory("RifleAmmo",1)
		TNT1 A 0 A_Takeinventory("64HighCaliberBox",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets2
		TNT1 A 0 A_Refire
		Goto Ready
	ADSAlternativeReload:
		AR2G A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",31,"AltADSFidget")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto Ready2
		TNT1 A 0 A_PlaySound("Relo1",CHAN_AUTO)
		AR2G ABCDE 1
		ARSR F 0 A_FireCustomMissile("EmptyClipSpawn",-5,0,8,-14)
		AR2G GHIJKLM 1
		AR2G N 4
		AR2G OP 1
		RIFL C 0 A_PlaySound("Relo2",CHAN_AUTO)
		AR2G O 1 A_PlaySound("Relo3",CHAN_AUTO)
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,3)
		TNT1 A 0 A_GiveInventory("RifleBolt",1)
		TNT1 A 0 A_JumpIfInventory("RifleBolt",1,"ADSReloadEmpty")
		AR2G NNMLKJIHGEDCB 1
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,"ADSInsertBullets2")//30+1 effect
	ADSReloadEmpty:
		TNT1 A 0 A_TakeInventory("RifleBolt",1)
		AR2G QRST 1
		AR2G U 3
		TNT1 A 0 A_PlaySound("ARSBOLT",CHAN_WEAPON)
		AR2G VWXYZ 1
		AR2G Z 4
		AR2G XYWV 1
		AR2G U 3
		AR2G TSRQB 1
	ADSInsertBullets:
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",30,"Ready2")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto Ready2
		TNT1 A 0 A_Giveinventory("RifleAmmo",1)
		TNT1 A 0 A_Takeinventory("64HighCaliberBox",1,TIF_NOTAKEINFINITE)
		Goto ADSInsertBullets
		TNT1 A 0 A_Refire
		Goto Ready2
	ADSInsertBullets2:
		TNT1 A 0 A_JumpIfInventory("RifleAmmo",31,"Ready2")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto Ready2
		TNT1 A 0 A_Giveinventory("RifleAmmo",1)
		TNT1 A 0 A_Takeinventory("64HighCaliberBox",1,TIF_NOTAKEINFINITE)
		Goto ADSInsertBullets2
		TNT1 A 0 A_Refire
		Goto Ready2
	AltFire:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"NoAim")
		TNT1 A 0 A_Giveinventory("Zoomed",1)
		TNT1 A 0 A_ZoomFactor(2.0)
		ARSZ A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFZ A 0
		"####" ABC 1
		Goto Ready2
	NoAim:
		ARSZ C 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RIFZ C 0
		"####" CBA 1
		TNT1 A 0 A_Takeinventory("Zoomed",0)
		TNT1 A 0 A_ZoomFactor(1.0)
		Goto Ready
	Ready2:
		AR2G A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RI2G A 0
		"####" A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		Loop
	FireZoomed:
		TNT1 A 0 A_FireCustomMissile("Tracer",0,0,0,-6)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,4,-5)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("RIFLE",CHAN_WEAPON)
		AR2F A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RI2F A 0
		"####" A 1 Bright A_FireBullets(0.0,0.0,1,7,"RifleBulletPuff",0)
		"####" B 1 A_SetPitch(-1.0 + pitch)
		"####" C 1 A_SetPitch(0.5 + pitch)
		AR2G A 0 A_JumpIfInventory("AltRifleSkin",1,2)
		RI2G A 0
		"####" A 1 A_SetPitch(0.5 + pitch)
		"####" A 0 A_Refire
		"####" A 5 A_WeaponReady
		Goto Ready2
	}
}