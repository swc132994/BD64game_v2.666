Actor AltPunchCharge : Inventory
{
inventory.maxamount 100
}

Actor 64Fist : Fist Replaces Fist
{
Weapon.SlotNumber 1
Weapon.SlotPriority 1
Weapon.SelectionOrder 3700
Tag "Fists"
Inventory.Icon "PUN1A0"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_P","Modern"
+INVENTORY.RESTRICTABSOLUTELY
+INVENTORY.UNDROPPABLE
+WEAPON.NOALERT
	States
	{
	Ready:
		PUN1 A 1 A_WeaponReady
		Loop
	Select:
		PUN1 A 1 A_Raise
		WAIT
	Deselect:
		PUN1 A 1 A_Lower
		Wait
	Fire:
		PUN1 A 1
		TNT1 A 0 A_Jump(256,"Punch1","Punch2")
	Punch1:
		PUN1 A 1
		TNT1 A 0 A_JumpIfInventory("powerstrength",1,"BerserkPunch1")
		PUN1 BCD 1
		PUN1 E 3 A_FireCustomMissile("PunchAttack",0,0,0,-7)
		PUN1 FGHI 1
		PUN1 JKA 1 A_WeaponReady(1)
		PUN1 A 4 A_WeaponReady(1)
		TNT1 A 0 A_ReFire
		Goto Ready
	Punch2:
		PUN1 A 1
		TNT1 A 0 A_JumpIfInventory("powerstrength",1,"BerserkPunch2")
		PUN2 BCD 1
		PUN2 E 3 A_FireCustomMissile("PunchAttack",0,0,0,-7)
		PUN2 FGHI 1
		PUN2 JK 1 A_WeaponReady(1)
		PUN1 A 5 A_WeaponReady(1)
		TNT1 A 0 A_ReFire
		Goto Ready
	BerserkPunch1:
		PUN4 BD 1
		PUN1 E 6 A_FireCustomMissile("SuperPunchAttack",0,0,0,-7)
		PUN1 FGHI 1
		PUN1 JKA 1 A_WeaponReady(1)
		PUN1 A 4 A_WeaponReady(1)
		TNT1 A 0 A_ReFire
		Goto Ready
	BerserkPunch2:
		PUN5 BD 1
		PUN2 E 6 A_FireCustomMissile("SuperPunchAttack",0,0,0,-7)
		PUN2 FGHI 1
		PUN2 JK 1 A_WeaponReady(1)
		PUN1 A 5 A_WeaponReady(1)
		TNT1 A 0 A_ReFire
		Goto Ready
	}
}

Actor 64FistTactical : Brutal64Weapon
{
Weapon.SlotNumber 1
Weapon.SlotPriority 1
Weapon.SelectionOrder 3700
Tag "Fists"
Inventory.Icon "PUN1A0"
Inventory.forbiddento "64DoomPlayer"
VisibleToPlayerClass "Tactical64DoomPlayer","Tactical64DoomPlayer_P","Modern"
+INVENTORY.RESTRICTABSOLUTELY
+INVENTORY.UNDROPPABLE
+WEAPON.NOALERT
	States
	{
	MineThrowFlash:
		PUN6 EFGH 1
		TNT1 A 18
		PUN6 ABCD 1
		Stop
	KickingFlash:
		PUN6 CBA 1
		TNT1 A 10
		PUN6 ABC 1
		Stop
	SlideKickingStart:
		PUN6 CBA 1
		TNT1 A 35
	SlideKickingEnd:
		TNT1 A 1
		PUN6 ABC 1
		Stop
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		PUN1 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		PUN1 LL 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		PUN1 MM 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		PUN1 NN 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		PUN1 OOO 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		PUN1 NN 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		PUN1 MM 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		PUN1 LL 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Loop
	Tired:
		TNT1 A 0 A_Print("Not enough stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",CHAN_VOICE)
		PUN1 A 12
		Goto Ready
	AltTired:
		TNT1 A 0 A_Print("Not enough stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",CHAN_VOICE)
		TNT1 A 0 A_TakeInventory("AltPunchCharge",0)
		PALT EDCBAAAAAA 2
		PALT AAAAAA 3
		Goto Ready
	Select:
		PUN6 ABCD 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_TakeInventory("AltPunchCharge",0)
		PUN6 DCBA 1
		TNT1 A 0 A_Lower
		Wait
	Fire:
		PUN1 A 1
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,2)
		TNT1 A 0 A_JumpIfInventory("isModern",1,3)
		Goto punch1
		TNT1 A 0 A_JumpIfInventory("Stamina",91,"Tired")
		TNT1 A 0 A_GiveInventory("Stamina",9)
		TNT1 A 0 A_Jump(256,"Punch1","Punch2")
	Punch1:
		PUN1 A 1
		TNT1 A 0 A_JumpIfInventory("powerstrength",1,"BerserkPunch1")
		PUN1 BCD 1
		PUN1 E 3 A_FireCustomMissile("PunchAttack",0,0,0,-7)
		PUN1 FGHI 1
		PUN1 JKA 1 A_WeaponReady(1)
		PUN1 A 4 A_WeaponReady(1)
		TNT1 A 0 A_ReFire
		Goto Ready
	Punch2:
		PUN1 A 1
		TNT1 A 0 A_JumpIfInventory("powerstrength",1,"BerserkPunch2")
		PUN2 BCD 1
		PUN2 E 3 A_FireCustomMissile("PunchAttack",0,0,0,-7)
		PUN2 FGHI 1
		PUN2 JK 1 A_WeaponReady(1)
		PUN1 A 5 A_WeaponReady(1)
		TNT1 A 0 A_ReFire
		Goto Ready
	BerserkPunch1:
		PUN4 BD 1
		PUN1 E 6 A_FireCustomMissile("SuperPunchAttack",0,0,0,-7)
		TNT1 A 0 A_RadiusThrust(5000,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		PUN1 FGHI 1
		PUN1 JKA 1 A_WeaponReady(1)
		PUN1 A 4 A_WeaponReady(1)
		TNT1 A 0 A_ReFire
		Goto Ready
	BerserkPunch2:
		PUN5 BD 1
		PUN2 E 6 A_FireCustomMissile("SuperPunchAttack",0,0,0,-7)
		TNT1 A 0 A_RadiusThrust(5000,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		PUN2 FGHI 1
		PUN2 JK 1 A_WeaponReady(1)
		PUN1 A 5 A_WeaponReady(1)
		TNT1 A 0 A_ReFire
		Goto Ready
	Reload: //Knuckle animation(Tactical and Modern only)
		PUNS ABC 1
		PUNS D 7 A_PlaySound("Knuckled",CHAN_BODY)
		PUNS EFED 3
		PUNS I 7 A_PlaySound("Knuckled",CHAN_BODY)
		PUNS JKJI 3
		Goto Ready
	Altfire:
		PALT ABCDE 1
	AltHold:
		TNT1 A 0 A_JumpIfInventory("IsModern",1,3)
		TNT1 A 0 A_JumpIfInventory("Stamina",100,"AltTired")
		TNT1 A 0 A_GiveInventory("Stamina",1)
		TNT1 A 0 A_GiveInventory("AltPunchCharge",1)
		PALT F 1
		TNT1 A 0 A_Refire
	AltEndPunch:
		TNT1 A 0 A_JumpIfInventory("AltPunchCharge",100,"AltEndPunch3")
		TNT1 A 0 A_JumpIfInventory("AltPunchCharge",50,"AltEndPunch2")
		PALT GHIJ 1
		TNT1 A 0 A_ZoomFactor(0.95)
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"BerserkAltEndPunch")
		TNT1 A 0 A_FireCustomMissile("AltPunchAttack",0,0,0,-7)
		PALT KL 1
		PALT M 2 Offset(0,32)
		PALT M 2 Offset(-1,34)
		TNT1 A 0 A_ZoomFactor(1.0)
		PALT NOPQRS 1
		TNT1 A 0 A_TakeInventory("AltPunchCharge",0)
		Goto Ready
	BerserkAltEndPunch:
		TNT1 A 0 A_ZoomFactor(0.95)
		TNT1 A 0 A_FireCustomMissile("SuperAltPunchAttack",0,0,0,-7)
		TNT1 A 0 A_RadiusThrust(5000,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		PALT KL 1
		PALT M 2 Offset(-1,34)
		TNT1 A 0 A_ZoomFactor(1.0)
		PALT NOPQRS 1
		TNT1 A 0 A_TakeInventory("AltPunchCharge",0)
		Goto Ready
	AltEndPunch2:
		PALT GHIJ 1
		TNT1 A 0 A_ZoomFactor(0.95)
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"BerserkAltEndPunch2")
		TNT1 AA 0 A_FireCustomMissile("AltPunchAttack",0,0,0,-7)
		PALT KL 1
		TNT1 A 0 A_RadiusThrust(750,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		TNT1 A 0 A_Print("Charged attack")
		PALT M 2 Offset(0,32)
		PALT M 2 Offset(-1,34)
		TNT1 A 0 A_ZoomFactor(1.0)
		PALT NOPQRS 1
		TNT1 A 0 A_TakeInventory("AltPunchCharge",0)
		Goto Ready
	BerserkAltEndPunch2:
		TNT1 A 0 A_ZoomFactor(0.95)
		TNT1 AA 0 A_FireCustomMissile("SuperAltPunchAttack",0,0,0,-7)
		TNT1 A 0 A_RadiusThrust(6000,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		TNT1 A 0 A_Print("Charged Berserk attack")
		PALT M 2 Offset(-1,34)
		TNT1 A 0 A_ZoomFactor(1.0)
		PALT NOPQRS 1
		TNT1 A 0 A_TakeInventory("AltPunchCharge",0)
		Goto Ready
	AltEndPunch3:
		PALT GHIJ 1
		TNT1 A 0 A_ZoomFactor(0.95)
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"BerserkAltEndPunch3")
		TNT1 AAA 0 A_FireCustomMissile("AltPunchAttack",0,0,0,-7)
		PALT KL 1
		TNT1 A 0 A_RadiusThrust(1500,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		TNT1 A 0 A_Print("Super charged attack")
		PALT M 2 Offset(0,32)
		PALT M 2 Offset(-1,34)
		TNT1 A 0 A_ZoomFactor(1.0)
		PALT NOPQRS 1
		TNT1 A 0 A_TakeInventory("AltPunchCharge",0)
		Goto Ready
	BerserkAltEndPunch3:
		TNT1 A 0 A_ZoomFactor(0.95)
		TNT1 AAA 0 A_FireCustomMissile("SuperAltPunchAttack",0,0,0,-7)
		TNT1 A 0 A_RadiusThrust(7000,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		TNT1 A 0 A_Print("Super charged Berserk attack")
		PALT M 2 Offset(-1,34)
		TNT1 A 0 A_ZoomFactor(1.0)
		PALT NOPQRS 1
		TNT1 A 0 A_TakeInventory("AltPunchCharge",0)
		Goto Ready
	}
}

Actor PunchAttack : FastProjectile
{
Damage(random(5,10))
DamageType "Melee"
Speed 27
Radius 14
Height 6
Alpha 0.6
RenderStyle Add
SeeSound "none"
DeathSound "none"
Decal "none"
Obituary "$OB_MPFIST"
+NOEXTREMEDEATH
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_PlaySound("VUP",CHAN_BODY)
		Stop
	Death:
		TNT1 A 0 A_PlaySound("player/cyborg/fist",CHAN_WEAPON)
		TNT1 AAAAAA 0 A_CustomMissile("MeleeSmoke",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 Radius_Quake(2,6,0,5,0)
		TNT1 A 10
		Stop
	}
}

Actor SuperPunchAttack : PunchAttack
{
Damage(2*random(60,80))
DamageType "SuperFists"
Obituary "$OB_MPFIST"
Decal "Crack"
+EXTREMEDEATH
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_PlaySound("weapons/gswing",CHAN_BODY)
		Stop
	Death:
		TNT1 A 0 A_PlaySound("FOOTWALL",CHAN_WEAPON)
		TNT1 AAAAAA 0 A_CustomMissile("MeleeSmoke",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 Radius_Quake(4,6,0,5,0)
		TNT1 A 15
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("SUPERFIST",CHAN_WEAPON)
		TNT1 AAAAAA 0 A_CustomMissile("MeleeSmoke",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 Radius_Quake(4,6,0,5,0)
		TNT1 A 15
		Stop
	}
}

Actor AltPunchAttack : PunchAttack
{
Damage(random(10,15))
}

Actor SuperAltPunchAttack : SuperPunchAttack
{
Damage(2*random(62,62))
}