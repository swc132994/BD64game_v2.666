ACTOR PistolSpawner : CustomInventory Replaces Pistol							//R31 ADDED PISTOL SPAWNER
{
Scale 0.82
Inventory.PickupMessage "$PICKUP_PISTOL_DROPPED"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		PIST A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64Pistol",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64PistolTactical",1)
		Stop
	}
}

Actor 64Pistol : DoomWeapon// Replaces Pistol									//R31 REMOVED REPLACES PISTOL
{
Scale 0.82
Weapon.SelectionOrder 1900
Weapon.AmmoUse 1
Weapon.AmmoGive 10																//R31 WAS 20
Weapon.AmmoType "Clip"
Obituary "$OB_MPPISTOL"
+WEAPON.WIMPY_WEAPON
+WEAPON.NOAUTOFIRE
Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
Tag "$TAG_PISTOL"
VisibleToPlayerClass "64DoomPlayer"												//R11
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_P","Modern"	//R11 ADDED //R15 Tactical64DoomPlayer_P	//R28 ADDED MODERN
+INVENTORY.RESTRICTABSOLUTELY
	states
	{
	Ready:
		PISG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,"Ready")					//R28.2
		TNT1 A 0 A_TakeInventory("Stamina",0)									//R28.2
		Loop
	Select:
		PISG A 1 A_Raise
		wait
	SelectAnimation:
		PISS ABCD 1
		Goto Ready
	Deselect:
		PISS DCBA 1
		TNT1 A 1 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 AA 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,0,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,1,10,"HitPuff")
		TNT1 A 0 A_PlaySound("EAGLEFI",1)
		TNT1 A 0 A_ZoomFactor(0.95)
		PISF A 1 BRIGHT
		TNT1 A 0 A_SetPitch(pitch-1.5)
		TNT1 A 0 A_ZoomFactor(1.0)
		PISF B 2
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,0,0)
		PISF CC 1 A_SetPitch(pitch+0.5)
		PISF D 1 A_SetPitch(pitch+0.5)
		PISG A 7 A_WeaponReady(1)
		TNT1 A 0 A_Refire
		Goto Ready
	Spawn:
		PIST A -1
		Stop
	}
}

///////////////////////////////////////
//pistol tactical						//R15 ADDED TACTICAL PISTOL
///////////////////////////////////////

ACTOR PistolAmmo : Ammo
{
Inventory.icon "PISTA0"
Ammo.BackPackAmount 0
Ammo.BackpackMaxAmount 10
Inventory.Amount 0
Inventory.MaxAmount 10
+IGNORESKILL
}

Actor 64PistolTACTICAL : DoomWeapon
{
Weapon.SelectionOrder 1901														//R21 WAS 1900
Weapon.AmmoUse1 1																//R25 WAS 0
Weapon.AmmoGive1 10
Weapon.AmmoType1 "Clip"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Weapon.AmmoType2 "PistolAmmo"
Obituary "$OB_MPPISTOL"
//+WEAPON.WIMPY_WEAPON															//R25
+WEAPON.NOAUTOFIRE
Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
Tag "$TAG_PISTOL"
Scale 0.82
Inventory.forbiddento "64DoomPlayer"
Inventory.restrictedto "Tactical64DoomPlayer","Tactical64DoomPlayer_P","Modern"	//R28 ADDED MODERN
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.AMMO_CHECKBOTH															//R25
	states
	{
	Spawn:
		PIST A -1
		Stop
	Select:
		PISG A 1 A_Raise
		WAIT
	SelectAnimation:
		PISS ABCD 1
		Goto Ready
	Deselect:
		PISS DCBA 1
		TNT1 A 1 A_Lower
		Wait
	NoAmmo:
		PISG A 1
		PISG A 10 A_PlaySound("DRYFIRE",1)
		Goto Ready
	Ready:
		PISG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,"Ready")					//R28.2
		TNT1 A 0 A_TakeInventory("Stamina",0)									//R28.2
		Loop
	Fire:
		TNT1 A 0 a_JumpifInventory("PistolAmmo",1,1)							//R24.2 WAS JUMP 2
		Goto Reload
		TNT1 A 0 a_JumpifInventory("clip",1,1)								//R25
		Goto Fire1															//R25
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,0,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,1,10,"HitPuff")
		TNT1 A 0 A_GiveInventory("Clip",1)									//R25
		TNT1 A 0 A_TakeInventory("PistolAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_PlaySound("EAGLEFI",CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(0.95)
		PISF A 1 BRIGHT
		TNT1 A 0 A_SetPitch(pitch-1.5)
		TNT1 A 0 A_ZoomFactor(1.0)
		PISF B 2
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,0,0)
		PISF CC 1 A_SetPitch(pitch+0.5)
		PISF D 1 A_SetPitch(pitch+0.5)
		PISG A 7 A_WeaponReady(1 | WRF_ALLOWRELOAD)
		TNT1 A 0 A_Refire
		Goto Ready
	Fire1:																		//R25 NEW FIRE1
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,0,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,1,10,"HitPuff")
		TNT1 A 0 A_TakeInventory("PistolAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_PlaySound("EAGLEFI",CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(0.95)
		PISF A 1 BRIGHT
		TNT1 A 0 A_SetPitch(pitch-1.5)
		TNT1 A 0 A_ZoomFactor(1.0)
		PISF B 2
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,0,0)
		PISF CC 1 A_SetPitch(pitch+0.5)
		PISF D 1 A_SetPitch(pitch+0.5)
		PISG A 7 A_WeaponReady(1 | WRF_ALLOWRELOAD)
		TNT1 A 0 A_Refire
		Goto Ready
	Reload:																		//R26 REVISED RELOAD ANIMATION OFFSETS
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_JumpIfInventory("PistolAmmo",10,"ready")
		TNT1 A 0 A_JumpIfInventory("Clip",1,1)			//R30.8 was jump 3
		Goto NoAmmo
		tnt1 A 0 A_playsound("reload")		//R31 RELOAD FROM IDDQD1337
		PISR ABCDEFGH 1
		TNT1 A 0 A_FireCustomMissile("EmptyPistolClipSpawn",-5,0,-10,-20)				//R30.10
		PISR I 10
		TNT1 A 0 A_PlaySound("prel")
		PISR JKLMNOPPP 1
		PISR QRSTUV 1
		Goto ReloadLoop
	ReloadLoop:
		TNT1 A 0 A_GiveInventory("PistolAmmo",1)
		TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("PistolAmmo",10,"Ready")
		TNT1 A 0 A_JumpIfInventory("Clip",1,"Reloadloop")
		Goto Ready
	DoKick:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("Kicking",1)
//		TNT1 A 0 A_Takeinventory("Zoomed",0)									//R11 UNUSED
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"ModernCheck")					//R28
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,1)						//R7 ADDED
		Goto Ready																//R7 ADDED
		TNT1 A 0 A_jumpifinventory("Stamina",20,3)
		TNT1 A 0 A_Print("Not enough stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",2)
		Goto Ready
		TNT1 A 0 A_TakeInventory("Stamina",20)
	ModernCheck:																//R28 ADDED
//		TNT1 A 0 A_Takeinventory("ADSmode",1)									//R11 UNUSED
		TNT1 A 0 A_JumpIf(momZ > 0,"AirKick")
		TNT1 A 0 A_JumpIf(momZ < 0,"AirKick")
		TNT1 A 0 A_JumpIf(pitch > 32,"LowKickChecker1")
		Goto InitializeNormalKick
	LowKickChecker1:
		TNT1 A 0 A_JumpIf(pitch > 90,"InitializeNormalKick")
		TNT1 A 0 A_FireCustomMissile("KickAttackDetectDowned",0,0,0,0)
	InitializeNormalKick:
		TNT1 A 0 A_jumpifinventory("PowerStrength",1,"BerserkerKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("KickAttack",0,0,0,-7)
		KICK H 5
		KICK I 1
		KICK GFEDCBA 1
		TNT1 A 0 SetPlayerProperty(0,0,0)
		Goto SelectAnimation
	BerserkerKick:
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("SuperKickAttack",0,0,0,-7)
		KICK H 5
		KICK I 1
		KICK GFEDCBA 1
		TNT1 A 0 SetPlayerProperty(0,0,0)
		Goto SelectAnimation
	AirKick:
		TNT1 A 0 A_TakeInventory("Stamina",5)									//R17 WAS 20
		TNT1 A 0 A_jumpifinventory("PowerStrength",1,"SuperAirKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 A_Recoil(-6)
		KICK JKLMN 1
		TNT1 A 0 A_FireCustomMissile("AirKickAttack",0,0,0,-31)
		KICK O 3
		KICK PQRST 2
		Goto SelectAnimation
	SuperAirKick:
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 A_Recoil(-6)
		KICK JKLMN 1
		TNT1 A 0 A_FireCustomMissile("SuperAirKickAttack",0,0,0,-31)
		KICK O 3
		KICK PQRST 2
		Goto SelectAnimation
	}
}