Actor 64Knightmare 19118
{
//$category "Game/BD64_Monsters
SpawnID 178
Scale 0.8
Health 500
Height 64
Radius 24
Mass 500
Speed 14
PainChance 75
Alpha 0.6
MeleeRange 60	//default 44
RenderStyle Translucent
DamageFactor "Head",0.8
Obituary "%o was charred by a Knightmare."
HitObituary "%o was mangled by a Knightmare."
SeeSound "knight/sight"
PainSound "knight/pain"
DeathSound "knight/death"
ActiveSound "knight/active"
Tag "Knightmare"
GibHealth 15
PainChance "SuperFists",255
PainChance "Kick",255
PainChance "SuperKick",255
DamageFactor "CauseWaterSplash",0.0
DamageFactor "CauseObjectsToSplash",0.0
DropItem "DemonEssenceSpawner"
BloodType "Brutal_BloodGreen"
MONSTER
+BOSSDEATH
+FLOORCLIP
+NOBLOODDECALS
+NEVERFAST
	states
	{
	Spawn:
		BOZ3 A 10 A_LookEx
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget20Green",12,0,70)
		Loop
	See:
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget2Green",0,0,70)
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItemEx("KnightmareSprite1",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItemEx("KnightmareSprite2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget2Green",0,0,70)
		TNT1 A 1 A_chase
		TNT1 A 0 A_SpawnItemEx("KnightmareSprite3",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 1 A_chase
		TNT1 A 0 A_SpawnItemEx("KnightmareSprite4",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget2Green",0,0,70)
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItemEx("KnightmareSprite1",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItemEx("KnightmareSprite2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget2Green",0,0,70)
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItemEx("KnightmareSprite3",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 1 A_chase("","")
		TNT1 A 0 A_SpawnItemEx("KnightmareSprite4",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget12Green",0,0,70)
		TNT1 AAAA 3 A_chase
		Loop
	Melee:
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget12Green",0,0,70)
		BOZ3 EF 6 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget6Green",0,0,70)
		BOZ3 G 6 A_CustomMeleeAttack(11*random(1,8),"baron/melee")
		Goto See
	Missile:
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget12Green",0,0,70)
		BOZ3 EF 6 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget6Green",0,0,70)
		BOZ3 G 6 Bright A_CustomMissile("64BaronBallYellow",32)
		Goto See
	Pain:
		BOZ3 H 3
		BOZ3 H 3 A_Pain
		Goto See
	Death:
		BOZ3 I 5 A_Scream
		BOZ3 J 5 A_NoBlocking
		BOZ3 KLM 5
		TNT1 A 0 A_KillMaster("None")	//tag 666 fix
		BOZ3 N 35 A_BossDeath
		BOZ3 N -1 A_SpawnItemEx("GrowingBloodPoolGreen")
		Stop
	Raise:
		BOZ3 NMLKJI 5
		TNT1 A 0 A_ChangeFlag("THRUACTORS",FALSE)
		Goto See
	Crush:
		TNT1 A 0 A_PlaySound("Misc/Gibbed",CHAN_BODY)
		POL5 A -1
		Stop
	Death.Plasma:
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Stop
		TNT1 AAAA 0 A_CustomMissile("XDeath1Green",32,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAA 0 A_CustomMissile("BloodmistSmallGreen",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",30,0,random(0,360),2,random(0,360))
		TNT1 AA 0 A_CustomMissile("CarbonizedSmoke1",30,0,random(0,360),2,random(10,90))
		TNT1 A 0 A_SpawnItemEx("BarrelExplosionSmokeColumn",random(-10,10),random(-10,10),20)
		TNT1 A 0 A_SpawnItemEx("BarrelExplosionSmokeColumn")
		TNT1 A 0 A_spawnItemEx("BurningFleshEffect2",0,0,5)
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		BOSB IJKLMN 4
		TNT1 A 0 A_KillMaster("None")	//tag 666 fix
		BOSB N -1 A_BossDeath
		Stop
	Death.SSG:
		BOZ3 H 2 A_Recoil(6)
		TNT1 A 0 A_JumpIfCloser(170,"XDeath")
		Goto Death
	Death.Head:
		TNT1 AA 0 A_CustomMissile("XDeath1Green",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2Green",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeathEyesGreen",40,0,random(0,360),2,random(-5,5))
		TNT1 AAAAAA 0 A_CustomMissile("SmallBrainPieceGreen",56,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath3Green",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		BZ1H ABCDE 5
		TNT1 A 0 A_KillMaster("None")	//tag 666 fix
		BZ1H F 35 A_BossDeath
		BZ1H F -1 A_SpawnItemEx("GrowingBloodPoolGreen")
		Stop
	Death.Cutless:
		TNT1 A 0 A_Jump(85,"Death")
	Death.SuperFists:
	Death.SuperKick:
		TNT1 A 0 A_Jump(170,"Death.Head")
	XDeath:
		TNT1 AAAAAA 0 A_CustomMissile("CeilBloodLauncherGreen",0,0,random(0,360),2,random(50,130))
		TNT1 AA 0 A_CustomMissile("XDeath1Green",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2Green",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath3Green",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeath5Green",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeathOrgan1Green",40,0,random(0,360),2,random(-5,5))
		TNT1 A 0 A_CustomMissile("XDeathOrgan2Green",40,0,random(0,360),2,random(-5,5))
		TNT1 AAAA 0 A_CustomMissile("IntestineGreen",32,0,random(0,360),2,random(0,90))
		TNT1 AAAA 0 A_CustomMissile("BloodmistGreen",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAA 0 A_CustomMissile("BloodmistBigGreen",45,0,random(0,360),2,random(10,90))
		TNT1 A 0 A_CustomMissile("KnightmareArm",52,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_SpawnItemEx("KnightmareLegs",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_XScream
		TNT1 A 0 A_BossDeath
		TNT1 A 0 A_KillMaster("None")	//tag 666 fix
		BZB1 A 2 A_Recoil(8)
		BZB1 A 2 ThrustThingZ(0,45,0,0)
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_Jump(128,2)
		BZB1 A 2 A_CheckFloor("LANDAFTERXDEATH1")
		Wait
		BZB1 C 2 A_CheckFloor("LANDAFTERXDEATH2")
		Wait
	LandAfterXDeath1:
		BZB1 B 1 A_Stop
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",TRUE)
		BZB1 B -1 A_SpawnItemEx("GrowingBloodPoolGreen")
		Stop
	LandAfterXDeath2:
		BZB1 D 1 A_Stop
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",TRUE)
		BZB1 D -1 A_SpawnItemEx("GrowingBloodPoolGreen")
		Stop
	}
}

Actor KnightmareLegs
{
Scale 0.8
Health 0
+CLIENTSIDEONLY
+THRUACTORS
+NOBLOCKMAP
+FLOORCLIP
+MOVEWITHSECTOR
+CORPSE
Alpha 0.6
RenderStyle Translucent
	States
	{
	Spawn:
		BZB3 A 0 NoDelay A_Jump(128,2)
		TNT1 A 0 A_SetScale(0-ScaleX,ScaleY)	//Flip horizontal
		BZB3 ABC 4
		TNT1 A 0 A_CustomMissile("BigBloodSpotGreen",0,0,random(0,360),2,random(0,90))
		BZB3 D -1
		Stop
	Crush:
		TNT1 A 0 A_PlaySound("Misc/Gibbed",CHAN_BODY)
		POL5 A -1
		Stop
	}
}

Actor KnightmareSprite1
{
Scale 0.8
Alpha 0.08
RenderStyle Translucent
+NOBLOCKMAP
+CLIENTSIDEONLY
+ISMONSTER
+FLOORCLIP
	States
	{
	Spawn:
		BOZ3 AAAA 1 A_FadeIn(0.09)
		BOZ3 A 4
		BOZ3 AAAAAA 1 A_FadeOut(0.09)
		Stop
	}
}

Actor KnightmareSprite2 : KnightmareSprite1
{
	States
	{
	Spawn:
		BOZ3 BBBB 1 A_FadeIn(0.09)
		BOZ3 B 4
		BOZ3 BBBBBB 1 A_FadeOut(0.09)
		Stop
	}
}

Actor KnightmareSprite3 : KnightmareSprite1
{
	States
	{
	Spawn:
		BOZ3 CCCC 1 A_FadeIn(0.09)
		BOZ3 C 4
		BOZ3 CCCCC 1 A_FadeOut(0.09)
		Stop
	}
}

Actor KnightmareSprite4 : KnightmareSprite1
{
	States
	{
	Spawn:
		BOZ3 DDDD 1 A_FadeIn(0.09)
		BOZ3 D 4
		BOZ3 DDDDDD 1 A_FadeOut(0.09)
		Stop
	}
}

Actor 64BaronBallYellow : BaronBall
{
Scale 0.75
Speed 20
FastSpeed 25
RenderStyle Normal
Decal "MedScorch"
Translation "0:255=%[0,0,0]:[1,1,0]"
	States
	{
	Spawn:
		BAL7 AB 2 NoDelay Bright A_CustomMissile("LensFlareProjectileYellow",-2,0)
		TNT1 A 0 A_CustomMissile("BaronYellowProjectileCore",-2,0)
		Loop
	Death:
		TNT1 A 1
		TNT1 AAA 0 A_CustomMissile("ExplosionParticleYellowHeavySmall",0,0,random(0,360),2,random(10,70))
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleYellowHeavySmall",0,0,random(0,360),2,random(20,50))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleYellowVeryFast",0,0,random(0,360),2,random(20,40))
		TNT1 AAAA 0 A_CustomMissile("ExplosionParticleYellow",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterMed")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterMed")
		BAL7 CDEFGH 1 Bright A_SpawnItemEx("LensFlareTorchYellow")
		Stop
	}
}

Actor BaronYellowProjectileCore
{
Translation "0:255=%[0,0,0]:[1,1,0]"
Scale 0.9
Alpha 0.9
RenderStyle Add
+CLIENTSIDEONLY
+THRUACTORS
+NOBLOCKMAP
+FLOORCLIP
	States
	{
	Spawn:
		BAL7 J 2 Bright
		Stop
	}
}