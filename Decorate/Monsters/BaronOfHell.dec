Actor 64BaronOfHell : BaronOfHell Replaces BaronOfHell
{
//$category "Game/BD64_Monsters
Scale 0.8
PainChance "SuperFists",200
PainChance "Kick",255
PainChance "SuperKick",255
DamageFactor "CauseWaterSplash",0.0
DamageFactor "CauseObjectsToSplash",0.0
DropItem "DemonEssenceSpawner"
MeleeRange 60	//default 44
BloodType "Brutal_BloodGreen"
BloodColor "00 64 00"	//Hex value
+NOBLOODDECALS
+NEVERFAST
	States
	{
	Spawn:
		BOSS A 10 A_LookEx
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget20GREEN",12,0,70)
		Loop
	See:
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget12GREEN",20,0,70)
		BOSS AABB 3 A_Chase
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget12GREEN",20,0,70)
		BOSS CCDD 3 A_Chase
		TNT1 A 0 A_JumpIfInTargetInventory("PowerInvisibility",1,"Forget")
		Loop
	Forget:
		TNT1 A 0 A_ClearTarget
		Goto Spawn
	Melee:
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget23GREEN",0,0,70)
		BOSS EF 8 A_FaceTarget
		BOSS G 8 A_CustomMeleeAttack(12*random(1,8),"baron/melee")	//Melee and Missile seperated
		Goto See
	Missile:
		TNT1 A 0 A_SpawnItemEx("HeadshotTarget23GREEN",0,0,70)
		BOSS EF 8 A_FaceTarget
		BOSS G 8 Bright A_CustomMissile("64BaronBall",32)	//Melee and Missile seperated
		Goto See
	Pain:
		BOSS H 2
		BOSS H 2 A_Pain
		Goto See
	Death:
		BOSS I 5 A_Scream
		BOSS J 5 A_NoBlocking
		BOSS KLM 5
		TNT1 A 0 A_KillMaster("None")	//tag 666 fix
		BOSS N 35 A_BossDeath
		BOSS N -1 A_SpawnItemEx("GrowingBloodPoolGreen")
		Stop
	Raise:
		BOSS NMLKJI 5
		TNT1 A 0 A_ChangeFlag("THRUACTORS",FALSE)
		Goto See
	Crush:
		TNT1 A 0 A_PlaySound("Misc/Gibbed",CHAN_BODY)
		A038 A -1
		Stop
/*	Stomp:		//unused state
		//BOSS A 1 A_FaceTarget
		//BOSS ABCD 4 A_Recoil(-1)
		BRST AAAAAABBCD 3 A_faceTarget
		BRST DDEF 3 A_FaceTarget
		TNT1 A 0 A_BasicAttack(40,"None","64BaronBall",32)
		BRST G 4
		Goto See*/
	Death.SSG:
		BOSS H 2 A_Recoil(6)
		TNT1 A 0 A_JumpIfCloser(170,"XDeath")
		Goto Death
	Death.Head:
		TNT1 AA 0 A_CustomMissile("XDeath1GREEN",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2GREEN",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeathEyesGreen",40,0,random(0,360),2,random(-5,5))
		TNT1 AAAAAA 0 A_CustomMissile("SmallBrainPieceGREEN",56,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath3GREEN",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		BS1H ABCDE 5
		TNT1 A 0 A_KillMaster("None")	//tag 666 fix
		BS1H F 35 A_BossDeath
		BS1H F -1 A_SpawnItemEx("GrowingBloodPoolGreen")
		Stop
	Death.Cutless:
		TNT1 A 0 A_Jump(85,"Death")
	Death.SuperFists:
	Death.SuperKick:
		TNT1 A 0 A_Jump(170,"Death.Head")
	XDeath:
		TNT1 AAAAAA 0 A_CustomMissile("CeilBloodLauncherGREEN",0,0,random(0,360),2,random(50,130))
		TNT1 AA 0 A_CustomMissile("XDeath1GREEN",52,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2GREEN",52,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath3GREEN",52,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeath5GREEN",52,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeathOrgan1GREEN",50,0,random(0,360),2,random(-5,5))
		TNT1 A 0 A_CustomMissile("XDeathOrgan2GREEN",50,0,random(0,360),2,random(-5,5))
		TNT1 AAAA 0 A_CustomMissile("IntestineGREEN",52,0,random(0,360),2,random(0,90))
		TNT1 AAAA 0 A_CustomMissile("BloodmistGREEN",55,0,random(0,360),2,random(0,90))
		TNT1 AAAAAA 0 A_CustomMissile("BloodmistBigGREEN",45,0,random(0,360),2,random(10,90))
		TNT1 A 0 A_CustomMissile("XDeathBaronARm",52,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_SpawnItemEx("XDeathBaronLegs",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_BossDeath
		TNT1 A 0 A_KillMaster("None")	//tag 666 fix
		BGB1 A 2 A_Recoil(8)
		BGB1 A 2 ThrustThingZ(0,45,0,0)
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_Jump(128,2)
		BGB1 A 2 A_CheckFloor("LANDAFTERXDEATH1")
		Wait
		BGB1 C 2 A_CheckFloor("LANDAFTERXDEATH2")
		Wait
	LandAfterXDeath1:
		BGB1 B 1 A_Stop
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",TRUE)
		BGB1 B -1 A_SpawnItemEx("GrowingBloodPoolGreen")
		Stop
	LandAfterXDeath2:
		BGB1 D 1 A_Stop
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",TRUE)
		BGB1 D -1 A_SpawnItemEx("GrowingBloodPoolGreen")
		Stop
	Death.Plasma:
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Stop
		TNT1 AAAA 0 A_CustomMissile("XDeath1GREEN",32,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAA 0 A_CustomMissile("BloodmistSmallGREEN",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",30,0,random(0,360),2,random(0,360))
		TNT1 AA 0 A_CustomMissile("CarbonizedSmoke1",30,0,random(0,360),2,random(10,90))
		TNT1 A 0 A_SpawnItemEx("BarrelExplosionSmokeColumn",random(-10,10),random(-10,10),20)
		TNT1 A 0 A_SpawnItemEx("BarrelExplosionSmokeColumn")
		TNT1 A 0 A_SpawnItemEx("BurningFleshEffect2",0,0,5)
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		BOSR IJKLMN 4
		TNT1 A 0 A_KillMaster("None")	//tag 666 fix
		BOSR N -1 A_BossDeath
		Stop
	Ice:
		"####" "#" 2 ACS_NamedExecuteAlways("IceDeathTranslate") 
		"####" "#" 5 A_FreezeDeath
		"####" "#" 0 A_KillMaster
		"####" "#" 1 A_FreezeDeathChunks
		wait
	}
}

Actor XDeathBaronLegs
{
XScale -0.8
YScale 0.8
Health 0
+CLIENTSIDEONLY
+THRUACTORS
+NOBLOCKMAP
+FLOORCLIP
+MOVEWITHSECTOR
+CORPSE
	States
	{
	Spawn:
		BGB3 A 0 NoDelay A_Jump(128,2)
		TNT1 A 0 A_SetScale(0-ScaleX,ScaleY)	//flip horizontal
		BGB3 ABC 4
		TNT1 A 0 A_CustomMissile("BigBloodSpotGREEN",0,0,random(0,360),2,random(0,90))
		BGB3 D -1
		Stop
	Crush:
		TNT1 A 0 A_PlaySound("Misc/Gibbed",CHAN_BODY)
		A038 A -1
		Stop
	}
}

Actor 64BaronBall : BaronBall
{
Scale 0.8
Radius 6
Height 6
RenderStyle Normal
Decal "MedScorch"
	States
	{
	Spawn:
		BRNR AB 2 NoDelay Bright A_CustomMissile("LensFlareProjectileOrange",-2,0)
		TNT1 A 0 A_CustomMissile("BaronProjectileCore",-2,0)
		Loop
	Death:
		TNT1 A 1
		TNT1 AAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(10,70))
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(20,50))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",0,0,random(0,360),2,random(20,40))
		TNT1 AAAA 0 A_CustomMissile("SmallFireballFireTrail",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterMed")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterMed")
		BRNR CDEFGH 1 Bright A_SpawnItemEx("LensFlareTorchOrange")
		Stop
	}
}

Actor BaronProjectileCore
{
Scale 0.9
Alpha 0.9
RenderStyle Add
+CLIENTSIDEONLY
+THRUACTORS
+NOBLOCKMAP
+FLOORCLIP
	States
	{
	Spawn:
		BAL7 I 2 Bright
		Stop
	}
}