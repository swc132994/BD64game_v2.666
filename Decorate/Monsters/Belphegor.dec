Actor 64Belphegor 11053
{
//$category "Game/BD64_Monsters
Scale 0.8
Health 1500
Height 64
Radius 24
Speed 8
FastSpeed 12
Mass 1200
SeeSound "64Bruiser/sight"
PainSound "64Bruiser/pain"
DeathSound "64Bruiser/death"
ActiveSound "64Bruiser/active"
Obituary "%o was Obliterated by a Belphegor."
Tag "Belphegor"
PainChance 25
PainChance "SuperFists",220
PainChance "Kick",255
PainChance "SuperKick",255
DamageFactor "CauseWaterSplash",0.0
DamageFactor "CauseObjectsToSplash",0.0
DropItem "DemonEssenceSpawner"
BloodType "Brutal_BloodGreen"
BloodColor "00 64 00"	//Hex value
MONSTER
+BOSSDEATH
+MOVEWITHSECTOR
+NOBLOODDECALS
+MISSILEMORE
+MISSILEEVENMORE
+FLOORCLIP
+NEVERFAST
	States
	{
	Spawn:
		BOS3 B 10 A_LookEx
		Loop
	See:
		BOS3 AAAABBBBCCCCDDDD 2 A_Chase
		Loop
	Pain:
		BOS3 H 10 A_Pain
		Goto See
	Missile:
		TNT1 A 0 A_JumpIfCloser(320,"ChargeStart")
		TNT1 A 0 A_PlaySound("64Bruiser/raiseguns",CHAN_AUTO)
		BOS3 EF 9 A_FaceTarget
		BOS3 G 8 BRIGHT A_CustomMissile("64BelphegorBall",60,0)
		Goto See
	ChargeStart:
		TNT1 A 0 A_PlaySound("KICK",CHAN_AUTO)
		TNT1 A 0 A_JumpIfCloser(90,"ChargeStop")
		BOS3 OOO 1 A_Chase
		TNT1 A 0 A_JumpIfCloser(90,"ChargeStop")
		BOS3 PPP 1 A_Chase
		TNT1 A 0 A_JumpIfCloser(90,"ChargeStop")
		BOS3 QQQ 1 A_Chase
		TNT1 A 0 A_JumpIfCloser(90,"ChargeStop")
		BOS3 RRR 1 A_Chase
		TNT1 A 0 A_JumpIfCloser(320,"ChargeStart")
		Goto See
	ChargeStop:
		TNT1 AAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("ExplosionAirDisplacementEffect")
		TNT1 A 0 A_Quake(4,8,0,100,"SUPERFIST")
		TNT1 A 0 A_PlaySound("FOOTWALL",CHAN_VOICE)
		BOS3 G 10 A_Explode(64,100,XF_NOTMISSILE,0,1)
		Goto See
	Melee:
		BOS3 EF 9 A_FaceTarget
		BOS3 G 8 A_CustomMeleeAttack(13*random(1,8),"baron/melee")	//Melee and Missile seperated
		Goto See
	Raise:
		TNT1 A 0 A_SetScale(0.8,0.8)
		BOS3 NMLKJI 5
		TNT1 A 0 A_ChangeFlag("THRUACTORS",FALSE)
		Goto See
	Death:
		BOS3 I 5 A_Scream
		BOS3 J 5 A_NoBlocking
		BOS3 KLM 5
		TNT1 A 0 A_KillMaster("None")	//tag 666 fix
		BOS3 N 35 A_BossDeath
		BOS3 N -1 A_CustomMissile("GrowingBloodPoolGreen",0,0,random(0,360),2,random(0,90))
		Stop
	Death.SSG:
		BOS3 H 2 A_Recoil(6)
		TNT1 A 0 A_JumpIfCloser(170,"XDeath2")
		Goto Death
	Death.Cutless:
		TNT1 A 0 A_Jump(92,"Death")
		Goto XDeath2
	Death.SuperFists:
	Death.SuperKick:
		TNT1 A 0 A_Jump(128,"Death")
		Goto XDeath2
	Death.BFG:
	Xdeath:	//Seperate legs
		TNT1 AAAAAA 0 A_CustomMissile("CeilBloodLauncherGreen",0,0,random(0,360),2,random(50,130))
		TNT1 AA 0 A_CustomMissile("XDeath1Green",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2Green",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath3Green",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeath5Green",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeathOrgan1Green",40,0,random(0,360),2,random(-5,5))
		TNT1 A 0 A_CustomMissile("XDeathOrgan2Green",40,0,random(0,360),2,random(-5,5))
		TNT1 AAAA 0 A_CustomMissile("IntestineGreen",32,0,random(0,360),2,random(0,90))
		TNT1 AAAA 0 A_CustomMissile("BloodmistGreen",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAA 0 A_CustomMissile("BloodmistBigGreen",45,0,random(0,360),2,random(10,90))
		TNT1 A 0 A_CustomMissile("BelphegorArm",52,0,random(0,360),2,random(0,90))
		TNT1 AAAA 0 A_CustomMissile("BelphegorLeg",52,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_BossDeath
		TNT1 A 0 A_KillMaster("None")	//tag 666 fix
		BPTG A 2 A_Recoil(8)
		BPTG A 2 ThrustThingZ(0,45,0,0)
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_Jump(128,2)
		BPTG A 2 A_CheckFloor("LANDAFTERXDEATH1")
		Wait
		BPTG C 2 A_CheckFloor("LANDAFTERXDEATH2")
		Wait
	Death.Chainsaw:
	Xdeath2:	//combined legs
		TNT1 AAAAAA 0 A_CustomMissile("CeilBloodLauncherGreen",0,0,random(0,360),2,random(50,130))
		TNT1 AA 0 A_CustomMissile("XDeath1Green",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath2Green",32,0,random(0,360),2,random(0,90))
		TNT1 AA 0 A_CustomMissile("XDeath3Green",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeath5Green",32,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_CustomMissile("XDeathOrgan1Green",40,0,random(0,360),2,random(-5,5))
		TNT1 A 0 A_CustomMissile("XDeathOrgan2Green",40,0,random(0,360),2,random(-5,5))
		TNT1 AAAA 0 A_CustomMissile("IntestineGreen",32,0,random(0,360),2,random(0,90))
		TNT1 AAAA 0 A_CustomMissile("BloodmistGreen",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAA 0 A_CustomMissile("BloodmistBigGreen",45,0,random(0,360),2,random(10,90))
		TNT1 A 0 A_CustomMissile("BelphegorArm",52,0,random(0,360),2,random(0,90))
		TNT1 A 0 A_SpawnItemEx("XDeathBelpLegs",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_BossDeath
		TNT1 A 0 A_KillMaster("None")	//tag 666 fix
		BPTG A 2 A_Recoil(8)
		BPTG A 2 ThrustThingZ(0,45,0,0)
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_Jump(128,2)
		BPTG A 2 A_CheckFloor("LANDAFTERXDEATH1")
		Wait
		BPTG C 2 A_CheckFloor("LANDAFTERXDEATH2")
		Wait
	LandAfterXDeath1:
		BPTG B 1 A_Stop
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",TRUE)
		BPTG B -1 A_CustomMissile("GrowingBloodPoolGreen",0,0,random(0,360),2,random(0,90))
		Stop
	LandAfterXDeath2:
		BPTG D 1 A_Stop
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",TRUE)
		BPTG D -1 A_CustomMissile("GrowingBloodPoolGreen",0,0,random(0,360),2,random(0,90))
		Stop
	Death.Plasma:
		TNT1 A 0 A_SetScale(1.0,1.0)
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Stop
		CRB1 A 1
		TNT1 AAAA 0 A_CustomMissile("XDeath1",32,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAA 0 A_CustomMissile("BloodmistSmall",35,0,random(0,360),2,random(0,90))
		TNT1 AAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",30,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",30,0,random(0,360),2,random(0,360))
		TNT1 AA 0 A_CustomMissile("CarbonizedSmoke1",30,0,random(0,360),2,random(10,90))
		TNT1 A 0 A_SpawnItemEx("BarrelExplosionSmokeColumn",random(-10,10),random(-10,10),20)
		TNT1 A 0 A_SpawnItemEx("BarrelExplosionSmokeColumn")
		TNT1 A 0 A_spawnItemEx("BurningFleshEffect2",0,0,5)
		TNT1 AAA 0 A_SpawnItemEx("Brutal_Blood",0,0,30)
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		CRB1 ABCDEFGH 4
		TNT1 A 0 A_KillMaster("None")
		CRB1 H -1 A_BossDeath
		Stop
	Crush:
		TNT1 A 0 A_PlaySound("Misc/Gibbed",CHAN_BODY)
		POL5 A -1
		Stop
	}
}

Actor XDeathBelpLegs
{
Scale 0.8
Health 0
+CLIENTSIDEONLY
+THRUACTORS
+NOBLOCKMAP
+FLOORCLIP
+MOVEWITHSECTOR
+CORPSE
	States
	{
	Spawn:
		BPBG A 0 NoDelay A_Jump(128,2)
		TNT1 A 0 A_SetScale(0-ScaleX,ScaleY)	//Flip horizontal
		BPBG ABCD 4
		TNT1 A 0 A_CustomMissile("BigBloodSpotGreen",0,0,random(0,360),2,random(0,90))
		BPBG E -1
		Stop
	Crush:
		TNT1 A 0 A_PlaySound("Misc/Gibbed",CHAN_BODY)
		POL5 A -1
		Stop
	}
}

Actor 64BelphegorBall : BaronBall
{
Translation "0:255=%[0,0,0]:[0,0,1]"//Blue
Damage 35
Scale 1.5
Speed 25
FastSpeed 30
Radius 6
Height 6
RenderStyle Normal
Decal "MedScorch"
+EXTREMEDEATH
	States
	{
	Spawn:
		BRNR AB 2 NoDelay Bright Light("PLASMABALL") A_CustomMissile("LensFlareProjectileBlue",-2,0)
		TNT1 A 0 A_CustomMissile("BaronProjectileCoreBlue",-2,0)
		Loop
	Death:
		TNT1 AAA 0 A_CustomMissile("ExplosionParticleBlueHeavySmall",0,0,random(0,360),2,random(10,70))
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleBlueHeavySmall",0,0,random(0,360),2,random(20,50))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleBlueVeryFast",0,0,random(0,360),2,random(20,40))
		TNT1 AAAA 0 A_CustomMissile("ExplosionParticleBlue",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterMed")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterMed")
		TNT1 A 0 Radius_Quake(2,54,0,15,0)
		TNT1 A 0 A_PlaySound("EXPLO3",CHAN_VOICE)
		TNT1 A 0 A_PlaySound("EXPLOSION",CHAN_WEAPON)
		TNT1 A 0 A_Explode(32,96,1)
		BAL7 CDEFGH 1 Bright Light("PLASMA_X3") A_SpawnItemEx("LensFlareTorchBlue")
		Stop
	}
}

Actor BaronProjectileCoreBlue
{
Translation "0:255=%[0,0,0]:[0,0,1]"//Blue
Scale 1.3
Alpha 1.3
RenderStyle Add
+CLIENTSIDEONLY
+THRUACTORS
+NOBLOCKMAP
+FLOORCLIP
	States
	{
	Spawn:
		BAL7 I 2 Bright
		Stop
	}
}