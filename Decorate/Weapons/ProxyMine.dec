Actor 64MineAmmo : Ammo
{
Scale 0.35
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 20
Inventory.Amount 1
Inventory.MaxAmount 10
Inventory.PickupMessage "You got a Multi-Purpose Mine!"
Inventory.Icon PMNED0
Inventory.PickupSound "IPICK"
	States
	{
	Spawn:
		PMNE D -1
		stop
	}
}

Actor MineHold : Inventory
{
inventory.maxamount 50
}

ACTOR 64ProximityMine : Brutal64Weapon
{
//$category "Game/BD64_Weapons
Scale 0.35
Weapon.SlotNumber 5
Weapon.SlotPriority 1
Weapon.SelectionOrder 9999
Weapon.AmmoType "64MineAmmo"
Weapon.AmmoUse 1
Weapon.AmmoGive 1
Obituary "%o was shreded by %k's Mine."
AttackSound "None"
Inventory.PickupSound "IPick"
Inventory.Pickupmessage "You got a Multi-Purpose Mine!"
Tag "Multi-Purpose Mine"
+WEAPON.NOAUTOAIM
+WEAPON.NOALERT
+WEAPON.NOAUTOFIRE
+WEAPON.ALT_USES_BOTH
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	KickingFlash:
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,1)
		Goto NoMinesKickingFlash
		PMNK ABCD 1
		PMNK E 8
		PMNK DCBA 1
		Stop
	AirKickingFlash:
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,1)
		Goto NoMinesAirKickingFlash
		PMNK ABCD 1
		PMNK E 10
		PMNK DCBA 1
		Stop
	SlideKickingStart:
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,1)
		Goto NoMinesSlideKickingStart
		PMNK ABCD 1
		PMNK E 18
	SlideKickingEnd:
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,1)
		Goto NoMinesSlideKickingEnd
		PMNK E 6
		PMNK DCBA 1
		Stop
	NoMinesKickingFlash:
		TNT1 A 16
		Stop
	NoMinesAirKickingFlash:
		TNT1 A 18
		Stop
	NoMinesSlideKickingStart:
		TNT1 A 22
	NoMinesSlideKickingEnd:
		TNT1 A 10
		Stop
	Spawn:
		PMNE C -1
		Stop
	Select:
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,1)
		Goto NoMines
		PMNS ABCDE 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_TakeInventory("MineHold",0)
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,1)
		Goto DeselectNoMines
		PMNS EDCBA 1
		TNT1 A 0 A_Lower
		Wait
	DeselectNoMines:
		TNT1 A 0 A_Lower
		Wait
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,1)
		Goto NoMines
		PMNE B 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		Loop
	NoMines:
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,"Select")
		TNT1 A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		Goto Ready
	Fire:	//Throw Active Mine, detonate on impact
		PMNF ABCD 1
	Hold:
		PMNF D 2
		TNT1 A 0 A_GiveInventory("MineHold",1)
		TNT1 A 0 A_Refire("Hold")
		PMNF EFG 1
		TNT1 A 0 A_JumpIfInventory("MineHold",50,"Throw3")
		TNT1 A 0 A_JumpIfInventory("MineHold",25,"Throw2")
	Throw1:
		TNT1 A 0 A_FireCustomMissile("64ThrownActiveProxMine1",0,1,0,0,0,10)
		TNT1 A 0 A_TakeInventory("MineHold",0)
		Goto FinishThrow
	Throw2:
		TNT1 A 0 A_FireCustomMissile("64ThrownActiveProxMine2",0,1,0,0,0,15)
		TNT1 A 0 A_TakeInventory("MineHold",0)
		Goto FinishThrow
	Throw3:
		TNT1 A 0 A_FireCustomMissile("64ThrownActiveProxMine3",0,1,0,0,0,20)
		TNT1 A 0 A_TakeInventory("MineHold",0)
		Goto FinishThrow
	FinishThrow:
		PMNF HIJKLMNOP 1
		TNT1 A 5
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,"Select")
		Goto NoMines
	AltFire:	//Place Proxy on ground
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,1)
		Goto NoMines
		PMNE B 1 Offset(0,33)
		PMNE B 1 Offset(0,34)
		PMNE B 1 Offset(0,35)
		PMNP ABCDE 1
		TNT1 A 0 A_TakeInventory("64MineAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 3
		TNT1 A 0 A_SpawnItemEx("64GroundProxMine",25)
		TNT1 A 0 A_JumpIfInventory("64MineAmmo",1,"Select")
		Goto NoMines
	}
}

Actor 64ThrownProxMine
{
Scale 0.35
Radius 10
Height 10
Speed 15
Damage(random(2,2))
BounceType "Doom"
BounceFactor 0.3
BounceCount 3
Gravity 0.95
SeeSound "GRNTOSS"
DeathSound "MINE002"
PROJECTILE
-NOTELEPORT
-BLOODSPLATTER
-NOGRAVITY
+TELESTOMP
+THRUGHOST
+THRUACTORS
+CANNOTPUSH
+FORCERADIUSDMG
+EXPLODEONWATER
+FIXMAPTHINGPOS
	States
	{
	Spawn:
		TNT1 A 0 NoDelay ThrustThingZ(0,random(12,14),0,1)
		TNT1 A 0 A_JumpIf(waterlevel>1,"Death")
		PMNE C 1
		TNT1 A 0 A_ChangeFlag("THRUACTORS",0)
		Goto Spawn+1
	Death:
	XDeath:
		TNT1 A 1 A_SpawnItemEx("64GroundProxMine")
		Stop
	}
}

Actor 64ThrownActiveProxMine1
{
Scale 0.35
Radius 10
Height 10
Health 5
Speed 20
Damage(random(2,2))
Gravity 1
SeeSound "GRNTOSS"
DeathSound "MINE002"
Decal "BigScorch"
Obituary "%o was shreded by %k's Mine."
ReactionTime 3
PROJECTILE
-NOTELEPORT
-BLOODSPLATTER
-NOGRAVITY
+TELESTOMP
+THRUGHOST
+CANNOTPUSH
+FORCERADIUSDMG
+EXPLODEONWATER
+FIXMAPTHINGPOS
+SKYEXPLODE
+BOUNCEONFLOORS
+BOUNCEONWALLS
+BOUNCEONCEILINGS
BounceFactor 0.5
WallBounceFactor 0.25
	States
	{
	Spawn:
		PMNE C 0 NoDelay ThrustThingZ(0,random(12,16),0,1)
		TNT1 A 0 A_JumpIf(waterlevel>1,"Death")
		PMNE CC 2 Light("BlurSphere") A_SpawnItemEx("RedFlareSpawn",0,0,6)
	Spawn2:
		TNT1 A 0 A_JumpIf(waterlevel>1,"Death")
		PMNE DDDDDDDD 2 Light("BlurSphere") A_SpawnItemEx("RedFlareSpawn",0,0,6)
		TNT1 A 0 A_Countdown
		Loop
	Xdeath:
	Death:
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick",CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx("RedFlareSpawn",0,0,6)
		PMNE D 4 Bright Light("BlurSphere")
		TNT1 A 0 A_SpawnItemEx("RedFlareSpawn",0,0,6)
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick",CHAN_BODY)
		PMNE D 4 Bright Light("BlurSphere")
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick",CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx("RedFlareSpawn",0,0,6)
		PMNE D 4 Bright Light("BlurSphere")
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick",CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx("RedFlareSpawn",0,0,6)
		PMNE D 4 Bright Light("BlurSphere")
		TNT1 A 1 ThrustThingZ(0,10,1,0)
		TNT1 A 0 Radius_Quake(2,54,0,15,0)
		TNT1 A 0 A_AlertMonsters(200)
		TNT1 A 0 A_PlaySound("Explosion",CHAN_ITEM)
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAA 0 A_CustomMissile("MediumExplosionFlames",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_Explode(200,175,XF_NOTMISSILE)
		TNT1 A 0 A_ChangeFlag("DONTSPLASH",1)
		TNT1 A 0 A_Explode(150,250,XF_NOTMISSILE)
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater")
		TNT1 A 0 A_SpawnItemEx("ExplosionFlareSpawner")
		TNT1 AAAAAAA 0 A_CustomMissile("ExplosionFlames",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 A 0 Radius_Quake(2,24,0,15,0)
		TNT1 A 0 A_PlaySound("FAREXPL",CHAN_ITEM)
		TNT1 AAAAA 1 A_CustomMissile("ExplosionSmoke",1,0,random(0,360),2,random(50,130))
		MISL BCDEF 1 Bright
		Stop
	}
}

Actor 64ThrownActiveProxMine2 : 64ThrownActiveProxMine1 {Speed 25}
Actor 64ThrownActiveProxMine3 : 64ThrownActiveProxMine1 {Speed 30}

Actor 64GroundProxMine : SwitchableDecoration
{
Scale 0.35
Height 6
Radius 10
Health 10
Damage 0
Mass 9999
Activation THINGSPEC_Activate
Tag "Proximity Mine"
PainChance "SuperFists",200
PainChance "Kick",255
PainChance "SuperKick",255
Damagetype Fire
Decal "BigScorch"
Obituary "%o was shreded by %k's Mine."
-SHOOTABLE
-SOLID
-EXPLODEONWATER
+FLOORCLIP
+FRIENDLY
+FIXMAPTHINGPOS
+MOVEWITHSECTOR
+NOBLOOD
+NOICEDEATH
+DONTRIP
+THRUACTORS
+LOOKALLAROUND
+QUICKTORETALIATE
+USESPECIAL
+EXTREMEDEATH
+FORCERADIUSDMG
	States
	{
	Pain:
		TNT1 A 0 A_FaceTarget
		TNT1 A 1 A_CustomMissile("64ThrownProxMine",5,0,random(170,190),2,random(20,40))
		Stop
	Active:
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick",CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx("BlueFlareSpawn",0,0,6)
		PMNE D 6 Light("MEGASPHERE")
		TNT1 A 0 A_SpawnItemEx("BlueFlareSpawn",0,0,6)
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick",CHAN_BODY)
		PMNE D 6 Light("MEGASPHERE")
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick",CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx("BlueFlareSpawn",0,0,6)
		PMNE D 6 Light("MEGASPHERE")
		PMNE D 4
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_ChangeFLag("SHOOTABLE",0)
		TNT1 A 1 A_SpawnItemEx("64MineAmmo")
		Stop
	Spawn:
		PMNE D 10
		TNT1 A 0 A_ChangeFlag("SHOOTABLE",1)
		TNT1 A 0 A_JumpIf(waterlevel>1,"Death")
		PMNE DDDD 8 A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick",CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx("YellowFlareSpawn",0,0,6)
		PMNE D 1 Light("YELLOWKEY") A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		TNT1 A 0 A_SpawnItemEx("YellowFlareSpawn",0,0,6)
		PMNE D 1 Light("YELLOWKEY") A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		TNT1 A 0 A_SpawnItemEx("YellowFlareSpawn",0,0,6)
		PMNE D 1 Light("YELLOWKEY") A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		TNT1 A 0 A_SpawnItemEx("YellowFlareSpawn",0,0,6)
		PMNE D 1 Light("YELLOWKEY") A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		TNT1 A 0 A_SpawnItemEx("YellowFlareSpawn",0,0,6)
		PMNE D 1 Light("YELLOWKEY") A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		TNT1 A 0 A_SpawnItemEx("YellowFlareSpawn",0,0,6)
		PMNE D 1 Light("YELLOWKEY") A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		TNT1 A 0 A_SpawnItemEx("YellowFlareSpawn",0,0,6)
		PMNE D 1 Light("YELLOWKEY") A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		TNT1 A 0 A_SpawnItemEx("YellowFlareSpawn",0,0,6)
		PMNE D 1 Light("YELLOWKEY") A_LookEx(LOF_NOSOUNDCHECK,0,200,0,0,"Death")
		Goto Spawn+1
	Xdeath:
	Death:
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick",CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx("RedFlareSpawn",0,0,6)
		PMNE D 4 Bright Light("BlurSphere")
		TNT1 A 0 A_SpawnItemEx("RedFlareSpawn",0,0,6)
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick",CHAN_BODY)
		PMNE D 4 Bright Light("BlurSphere")
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick",CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx("RedFlareSpawn",0,0,6)
		PMNE D 4 Bright Light("BlurSphere")
		TNT1 A 0 A_PlaySound("Weapons/StickyBombTick",CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx("RedFlareSpawn",0,0,6)
		PMNE D 4 Bright Light("BlurSphere")
		TNT1 A 0 Radius_Quake(2,54,0,15,0)
		TNT1 A 0 A_ChangeFlag("FLOORCLIP",0)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("Explosion",CHAN_ITEM)
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAA 0 A_CustomMissile("MediumExplosionFlames",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_Explode(200,175,XF_NOTMISSILE)
		TNT1 A 0 A_Explode(150,250,XF_NOTMISSILE)
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater")
		TNT1 A 0 A_SpawnItemEx("ExplosionFlareSpawner")
		TNT1 AAAAAAA 0 A_CustomMissile("ExplosionFlames",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 A 0 Radius_Quake(2,24,0,15,0)
		TNT1 A 0 A_PlaySound("FAREXPL",CHAN_ITEM)
		TNT1 A 0 A_SpawnItemEx("BarrelExplosionSmokeColumn")
		TNT1 AAAAA 1 A_CustomMissile("ExplosionSmoke",1,0,random(0,360),2,random(50,130))
		MISL BCDEF 1 Bright
		Stop
	}
}