Actor ShotgunSpawner : CustomInventory	replaces Shotgun	//spawned from randomspawners
{
Scale 0.87
Tag "Shotgun"
Inventory.PickupMessage "$GOTSHOTGUN"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		SHOT A 2
		TNT1 A 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		SHOT A 2
		TNT1 A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		SHOT A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64Shotgun",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64ShotgunTactical",1)
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64Shotgun")
		TNT1 A 0 A_SpawnItemEx("64ShotgunTACTICAL")
		TNT1 A 1050
		Loop
	}
}

Actor 64Shotgun : DoomWeapon
{
Scale 0.87
Weapon.SlotNumber 3
Weapon.SlotPriority 1
Weapon.SelectionOrder 1300
Weapon.AmmoType "Shell"
Weapon.AmmoUse 1
Weapon.AmmoGive 8
Inventory.PickupMessage "$GOTSHOTGUN"
Obituary "$OB_MPSHOTGUN"
Tag "$TAG_SHOTGUN"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Ready:
		CHTG A 0 A_JumpIfInventory("VWeaponMode",1,2)
		SHTG A 0
		"####" A 1 A_WeaponReady
		Wait
	Select:
		CHTG A 0 A_JumpIfInventory("VWeaponMode",1,2)
		SHTG A 0
		"####" A 1 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		CHTG A 0 A_JumpIfInventory("VWeaponMode",1,2)
		SHTG A 0
		"####" A 1 A_Lower
		Wait
	Fire:
		CHTG A 0 A_JumpIfInventory("VWeaponMode",1,2)
		SHTG A 0
		"####" A 1
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_JumpIfInventory("VWeaponMode",1,"VanillaFire")
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,10,5,"ShotgunPuff")
		TNT1 A 0 A_PlaySound("weapons/shotgf",CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(0.95)
		SHTF AB 2 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		SHTF C 1
		SHTG NOP 1
		SHTG Q 1 A_FireCustomMissile("ShotCaseSpawn",0,0,1,-9)
		SHTG RSTUUUTSRQPON 1
		SHTF FG 1
		TNT1 A 0 A_Refire
		Goto Ready
	VanillaFire:
		TNT1 A 0 A_GunFlash("VanillaFireFlash")
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,10,5,"ShotgunPuff")
		TNT1 A 0 A_PlaySound("weapons/shotgf",CHAN_WEAPON)
		CHTG A 5
		CHTG B 15
		TNT1 A 0 A_FireCustomMissile("ShotCaseSpawn",0,0,1,-9)
		CHTG C 7
		CHTG A 8
		TNT1 A 0 A_Refire
		Goto Ready
	VanillaFireFlash:
		CHTF A 1 Bright A_Light1
		CHTF A 0 A_Light0
		Goto LightDone
	Spawn:
		SHOT A -1
		Stop
	}
}

Actor ShotgunAmmo : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 9
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 9
Inventory.Icon "shota0"
+IGNORESKILL
}

Actor HasUsedShell : Inventory
{
inventory.maxamount 1
}

Actor NeverSelectedShotgun : Inventory
{
inventory.maxamount 1
}

Actor 64ShotgunTACTICAL : Brutal64Weapon
{
Scale 0.87
Weapon.SlotNumber 3
Weapon.SlotPriority 1
Weapon.SelectionOrder 1300
Weapon.AmmoType1 "Shell"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 8
Weapon.AmmoType2 "ShotgunAmmo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.PickupMessage "$GOTSHOTGUN"
Obituary "$OB_MPSHOTGUN"
Tag "$TAG_SHOTGUN"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.AMMO_CHECKBOTH
+NOALERT
	States
	{
	MineThrowFlash:
		SHTS EFGH 1
		TNT1 A 18
		SHTS ABCD 1
		Stop
	KickingFlash:
		SHTG BCDEF 1
		SHTG G 6
		SHTG FEDCB 1
		Stop
	ReloadingKick:
		SHSR C 16
		Stop
	AirKickingFlash:
		SHTG BCDEF 1
		SHTG G 8
		SHTG FEDCB 1
		Stop
	SlideKickingStart:
		SHTG BCDEF 1
		SHTG G 10
	SlideKickingEnd:
		SHTG G 5
		SHTG FEDCB 1
		Stop
	Spawn:
		SHOT A -1
		Stop
	WarmUp:
		TNT1 A 0 A_TakeInventory("NeverSelectedShotgun")
		SHTG E 1 Offset(80,90)
		SHTG E 1 Offset(73,75)
		SHTG E 1 Offset(69,69)
		SHTG E 1 Offset(65,65)
		SHTG E 1 Offset(60,57)
		SHTG E 1 Offset(57,54)
		SHTG E 1 Offset(54,50)
		SHTG E 1 Offset(50,40)
		SHTG E 1 Offset(43,36)
		SHTG F 1 Offset(35,35)
		SHTG F 1 Offset(30,34)
		SHTG G 1 Offset(15,33)
		SHTG G 1 Offset(5,31)
		SHTG G 1 A_WeaponReady
		SHTG H 1 A_PlaySound("Shotgun/PumpIn",CHAN_ITEM)
		SHTG IJKLM 1
		SHTG M 1 Offset(-2,35)
		SHTG M 1 Offset(-4,38)
		SHTG M 1 Offset(-6,45)
		SHTG M 1 Offset(-4,38)
		SHTG M 1 Offset(-2,35)
		SHTG M 1 Offset(0,33) A_PlaySound("Shotgun/PumpOut",CHAN_ITEM)  
		SHTG MLKJIHGFEDCB 1 A_WeaponReady
		Goto Ready
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
	OkToFire:
		SHTG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Goto Ready
	NoAmmo:
		SHTG A 10
		TNT1 A 0 A_PlaySound("DRYFIRE",CHAN_WEAPON)
		TNT1 A 0 A_TakeInventory("HasUsedShell",0)
		TNT1 A 0 A_TakeInventory("ShotgunPumping",0)
		Goto Ready
	Select:
		TNT1 A 0 A_JumpIfInventory("NeverSelectedShotgun",1,"WarmUp")
		TNT1 A 0 A_JumpIfInventory("HasUsedShell",1,2)
		TNT1 A 0 A_PlaySound("Shotgun/Pump",CHAN_ITEM)
		SHTS ABCD 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_TakeInventory("ShotgunPumping",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		SHTS EFGH 1
		TNT1 A 0 A_Lower
		Wait
	Fidget:
		SHTG BCDEFG 1 A_WeaponReady(WRF_NOFIRE)
		SHTG H 1 A_PlaySound("Shotgun/PumpIn",CHAN_ITEM)
		SHTG IJJ 1 A_WeaponReady(WRF_NOFIRE)
		SHTG J 15 A_WeaponReady(WRF_NOFIRE)
		SHTG J 1 A_PlaySound("Shotgun/PumpOut",CHAN_ITEM)
		SHTG JIH 1 A_WeaponReady(WRF_NOFIRE)
		SHTG HFEDCB 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("ShotgunPumping",1,2)
		TNT1 A 0 A_JumpIfInventory("HasUsedShell",1,"Pump")
		Goto AmmoCheck
		SHTG GFEDCBA 1
		TNT1 A 0 A_TakeInventory("ShotgunPumping",0)
		Goto AmmoCheck
	ResumeFire:
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,10,5,"ShotgunPuff",0)
		TNT1 A 0 A_GiveInventory("HasUsedShell",1)
		TNT1 A 0 A_PlaySound("SGFIRE",CHAN_WEAPON)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ZoomFactor(0.95)
		SHTF AB 2 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		SHTF C 1 A_SetPitch(pitch-2,SPF_INTERPOLATE)
		SHTF D 1 A_SetPitch(pitch+0.8,SPF_INTERPOLATE)
		SHTF E 1 A_SetPitch(pitch+0.6,SPF_INTERPOLATE)
		SHTF F 1 A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		SHTF G 1 A_SetPitch(pitch+0.2,SPF_INTERPOLATE)
		SHTG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"Pump")
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,2)
		Goto ReloadNormally
	Pump:
		TNT1 A 0 A_TakeInventory("ShotgunPumping",0)
		SHTG BCDEFG 1 A_WeaponReady(WRF_NOPRIMARY)
		TNT1 A 0 A_FireCustomMissile("ShotCaseSpawn",0,0,-4,-4)
		SHTG H 1 A_PlaySound("Shotgun/PumpIn",CHAN_ITEM)
		SHTG IJ 1
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,2)
		TNT1 A 0 A_Giveinventory("HasUsedShell",1)
		Goto ReloadNormally
		SHTG KLM 1
		SHTG M 1 A_PlaySound("Shotgun/PumpOut",CHAN_ITEM)
		SHTG LKJ 1
		TNT1 A 0 A_Takeinventory("HasUsedShell",1)
		SHTG IHGFEDCB 1 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,1)
		Goto ReadyToFireAgain
	ReadyToFireAgain:
		SHTG A 1 Offset(10,36)
		SHTG A 1 Offset(9,40)
		SHTG A 1 Offset(4,40)
		SHTG A 1 Offset(0,32)
		TNT1 A 0 A_Refire
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto NoAmmo
		TNT1 A 0 A_ZoomFactor(1.0)
		SHTG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("HasUsedShell",1,"Pump")
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",9,"Fidget")
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,"ReloadNormally")
		TNT1 A 0 A_GiveInventory("HasUsedShell",1)
	ReloadNormally:
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto Ready
		SHTG BCDEFG 1
		SHSR ABC 1
	InsertingShells:
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto PumpNewShell
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",8,"CheckIfFinishReload")	//reloads 8 if shotgun completly empty
		TNT1 A 0 A_GiveInventory("ShotgunAmmo",1)
		TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
	InsertShellAnimation:
		SHSR C 3
		SHTR ABC 1
		SHTR D 1 A_PlaySound("inshell",CHAN_AUTO)
		TNT1 A 0 A_GiveInventory("ShotgunPumping",0)
		SHTR EFGHIJ 1
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKickReloading")
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",9,"FinishInsertingShells")
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,1)
		Goto InsertingShells
		SHTR J 1 A_WeaponReady(WRF_NOBOB)
		Goto InsertingShells
	CheckIfFinishReload:
		SHSR C 1
		TNT1 A 0 A_JumpIfInventory("HasUsedShell",1,"FinishInsertingShells")
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto PumpNewShell
		TNT1 A 0 A_Giveinventory("ShotgunAmmo",1)
		TNT1 A 0 A_Takeinventory("Shell",1,TIF_NOTAKEINFINITE)
		Goto InsertShellAnimation
	FinishInsertingShells:
		SHSR CBA 1
		SHTG G 1 A_TakeInventory("ShotgunPumping",0)
		TNT1 A 0 A_JumpIfInventory("HasUsedShell",1,"PumpNewShell")
		SHTG FEDCB 1
		Goto Ready
	PumpNewShell:
		TNT1 A 0 A_ZoomFactor(1.0)
		SHTG G 1
		TNT1 A 0 A_FireCustomMissile("ShotCaseSpawn",0,0,-4,-4)
		SHTG H 1 A_PlaySound("Shotgun/PumpIn",CHAN_BODY)
		SHTG IJKLMM 1
		SHTG L 1 A_PlaySound("Shotgun/PumpOut",CHAN_BODY)
		SHTG KJ 1
		TNT1 A 0 A_TakeInventory("HasUsedShell",1)
		SHTG IHGFEDCB 1
		Goto ReadyToFireAgain
	DoKickReloading:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("Kicking",1)
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"ModernCheckReloading")
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,1)
		Goto Ready
		TNT1 A 0 A_JumpIfInventory("Stamina",80,"Tired")
		TNT1 A 0 A_GiveInventory("Stamina",20)
	ModernCheckReloading:
		TNT1 A 0 A_JumpIf(pitch > 32,"LowKickReloadingChecker1")
		Goto InitializeReloadingKick
	LowKickReloadingChecker1:
		TNT1 A 0 A_JumpIf(pitch > 90,"InitializeReloadingKick")
		TNT1 A 0 A_FireCustomMissile("KickAttackDetectDowned",0,0)
	InitializeReloadingKick:
		TNT1 A 0 A_GunFlash("ReloadingKick")
		TNT1 A 0 A_PlaySound("KICK",CHAN_AUTO)
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("KickAttack",0,0,0,-7)
		KICK H 5
		KICK IGFEDCBA 1
		TNT1 A 0 SetPlayerProperty(0,0,0)
		Goto FinishInsertingShells
	Tired:
		TNT1 A 0 A_Print("Not Enough Stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",CHAN_VOICE)
		SGAA H 15
		Goto Ready
	Altfire:
		SGAA ABCD 1
	AltHold:
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"AltHold2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,3)
		TNT1 A 0 A_JumpIfInventory("Stamina",85,"Tired")
		TNT1 A 0 A_GiveInventory("Stamina",15)
		TNT1 A 0 A_Playsound("SGSWING",CHAN_AUTO)
		SGAA E 1 A_FireCustomMissile("KickAttack",0,0,0,-7)
		TNT1 A 0 A_RadiusThrust(4500,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		SGAA EFGH 1
		SGAA IJ 3
		SGAA KLMN 2
		TNT1 A 3
		SGAA CBA 1 A_Refire
		SGAA A 1 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
	AltHold2:
		TNT1 A 0 A_JumpIfInventory("IsModern",1,3)
		TNT1 A 0 A_JumpIfInventory("Stamina",88,"Tired")
		TNT1 A 0 A_GiveInventory("Stamina",12)
		TNT1 A 0 A_Playsound("SGSWING2",CHAN_AUTO)
		SGAA N 1 A_FireCustomMissile("SuperKickAttack",0,0,0,-7)
		TNT1 A 0 A_RadiusThrust(6750,65,RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE)
		SGAA NMLK 1
		SGAA JI 3
		SGAA HGFE 1
		TNT1 A 3
		SGAA CBA 1 A_ReFire
		SGAA A 1 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
	AmmoCheck:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_TakeInventory("ShotgunAmmo",1,TIF_NOTAKEINFINITE)
		Goto ResumeFire
	NoReload:
		TNT1 A 0 A_JumpIfInventory("Shell",1,1)
		Goto NoAmmo
		TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
		Goto ResumeFire
	}
}

Actor ShotgunPumping : Inventory
{
Inventory.MaxAmount 1
}
