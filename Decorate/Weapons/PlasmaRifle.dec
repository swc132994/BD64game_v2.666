Actor PlasmaRifleSpawner : CustomInventory// Replaces PlasmaRifle		//spawned from randomspawners
{
//$category "Game/BD64_Weapons
Scale 0.82
Tag "Plasma Rifle"
Inventory.PickupMessage "$GOTPLASMA"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		PLAS A 2
		TNT1 A 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		PLAS B 2
		TNT1 A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		PLAS C 2
		Loop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64PlasmaRifle",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64PlasmaRifleTACTICAL",1)
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64PlasmaRifle")
		TNT1 A 0 A_SpawnItemEx("64PlasmaRifleTACTICAL")
		TNT1 A 1050
		Loop
	}
}

Actor HasPlasmaWeapon : Inventory
{
inventory.maxamount 1
}

Actor 64PlasmaRifle : DoomWeapon
{
Scale 0.82
Weapon.SlotNumber 6
Weapon.SlotPriority 2
Weapon.SelectionOrder 100
Weapon.AmmoType "Cell"
Weapon.AmmoUse 1
Weapon.AmmoGive 40
Inventory.PickupMessage "$GOTPLASMA"
Tag "$TAG_PLASMARIFLE"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Select:
		PLSG A 1 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_TakeInventory("HasPlasmaWeapon",0)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		PLSG A 1 A_Lower
		Wait
	Ready:
		TNT1 A 0 A_PlaySound("weapon/plasmaidle",CHAN_WEAPON,1,TRUE)
		PLSG ABC 1 A_WeaponReady
		Loop
	Fire:
		TNT1 A 0 A_GiveInventory("HasPlasmaWeapon",1)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,22)
		PLSF A 1 Bright A_FireCustomMissile("64PlasmaBall",0,1,0,-5)
		PLSF B 1 Bright
		PLSF E 2
		TNT1 A 0 A_ReFire
		TNT1 A 0 A_JumpIfInventory("VWeaponMode",1,"Ready")
		TNT1 A 0 A_SpawnItemEx("PlasmaSmoke",30,0,40,0,0,2)
		PLSF C 1 offset(0,46)
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		PLSF C 1 offset(0,43)
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		PLSF D 1 offset(0,40)
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		PLSF D 1 offset(0,37)
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		PLSF E 1 offset(0,34)
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		PLSF E 1 offset(0,32)
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto Ready
	Spawn:
		PLAS ABC 2
		Loop
	}
}

Actor PlasmaRifleAmmo : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 40
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 40
Inventory.Icon "plasa0"
+IGNORESKILL
}

Actor 64PlasmaRifleTACTICAL : Brutal64Weapon
{
Scale 0.82
Weapon.SlotNumber 6
Weapon.SlotPriority 2
Weapon.SelectionOrder 100
Weapon.AmmoType1 "Cell"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 40
Weapon.AmmoType2 "PlasmaRifleAmmo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.PickupMessage "$GOTPLASMA"
Tag "$TAG_PLASMARIFLE"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.AMMO_CHECKBOTH
+WEAPON.NOALERT
+WEAPON.NOAUTOFIRE
	States
	{
	MineThrowFlash:
		PLSG NOPQ 1
		TNT1 A 18
		PLSG JKLM 1
		Stop
	KickingFlash:
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",1,1)	//Cooldown if no ammo
		Goto KickingFlashNoAmmo
		PLSR A 0 A_JumpIfInventory("PlasmaRifleAmmo",21,2)	//Pseudo-Fade Effect
		2LSR A 0
		"####" ABCDEF 1
		"####" G 4
		"####" FEDCBA 1
		Stop
	AirKickingFlash:
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",1,1)	//Cooldown if no ammo
		Goto AirKickingFlashNoAmmo
		PLSR A 0 A_JumpIfInventory("PlasmaRifleAmmo",21,2)	//Pseudo-Fade Effect
		2LSR A 0
		"####" ABCDEF 1
		"####" G 6
		"####" FEDCBA 1
		Stop
	KickingFlashNoAmmo:
		3LSR ABCDEF 1
		3LSR G 4
		3LSR FEDCBA 1
		Stop
	AirKickingFlashNoAmmo:
		3LSR ABCDEF 1
		3LSR G 6
		3LSR FEDCBA 1
		Stop
	SlideKickingStart:
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",1,1)	//Cooldown if no ammo
		Goto SlideKickingStartNoAmmo
		PLSR A 0 A_JumpIfInventory("PlasmaRifleAmmo",21,2)	//Pseudo-Fade Effect
		2LSR A 0
		"####" ABCDEF 1
		"####" G 10
	SlideKickingEnd:
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",1,1)	//Cooldown if no ammo
		Goto SlideKickingEndNoAmmo
		PLSR A 0 A_JumpIfInventory("PlasmaRifleAmmo",21,2)	//Pseudo-Fade Effect
		2LSR A 0
		"####" G 4
		"####" FEDCBA 1
		Stop
	SlideKickingStartNoAmmo:
		3LSR ABCDEF 1
		3LSR G 10
	SlideKickingEndNoAmmo:
		3LSR G 4
		3LSR FEDCBA 1
		Stop
	Select:
		TNT1 A 0 A_PlaySound("PLSDRAW",CHAN_BODY)
		PLSG JKLM 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_TakeInventory("HasPlasmaWeapon",0)
		PLSG NOPQ 1
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_Lower
		Wait
	Spawn:
		PLAS ABC 2
		Loop
	v1Ready:
	Ready:
		TNT1 A 0 A_PlaySound("weapon/plasmaidle",CHAN_WEAPON,1,TRUE)
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",1,1)	//Cooldown if no ammo
		Goto NoAmmo
		PLSG A 0 A_JumpIfInventory("PlasmaRifleAmmo",21,2)	//Pseudo-Fade Effect
		2LSG A 0
		"####" ABC 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	NoAmmo:
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",1,"Ready")
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		3LSG A 5 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		TNT1 A 0 A_GiveInventory("HasPlasmaWeapon",1)
		Goto AmmoCheck
	ResumeFire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,22)
		PLSF A 1 Bright A_FireCustomMissile("64PlasmaBall",0,0,0,-5)
		PLSF B 1 Bright
		PLSF E 2 A_SetPitch(pitch-1.0,SPF_INTERPOLATE)
		TNT1 A 0 A_ReFire
		TNT1 A 0 A_SpawnItemEx("PlasmaSmoke",30,0,40,0,0,2)
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",1,1)	//Cooldown if no ammo
		Goto NoAmmoCooldown
		PLSR A 0 A_JumpIfInventory("PlasmaRifleAmmo",21,2)	//Pseudo-Fade Effect
		2LSR A 0
		"####" ABCDEF 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		"####" G 8 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		"####" FEDCBA 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Refire
		Goto Ready
	NoAmmoCooldown:
		3LSR ABCDEF 1
		3LSR G 8
		3LSR FEDCBA 1
		Goto NoAmmo
	Reload:
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		Goto Ready
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",40,"Ready")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		Goto NoAmmo
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		11SR A 0 A_JumpIfInventory("PlasmaRifleAmmo",21,2)	//Pseudo-Fade Effect
		33SR A 0
		"####" AB 1 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_PlaySound("PLREAD1",CHAN_WEAPON)
		11SR C 0 A_JumpIfInventory("PlasmaRifleAmmo",21,2)	//Pseudo-Fade Effect
		33SR C 0
		"####" CDEFGHIJ 1
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",20,"Reloadcell2")
		33SR K 1 A_FireCustomMissile("PlasmaCaseSpawn",-5,0,-10,-15)
		33SR K 10 A_PlaySound("PLREL",CHAN_AUTO)
		33SR LMNOPQRSTUVWXYZ 1
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",20,"Reloadcell2")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		Goto Reloadcell2
		TNT1 A 0 A_GiveInventory("PlasmaRifleAmmo",1)
		TNT1 A 0 A_Takeinventory("Cell",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets
	Reloadcell2:
		TNT1 A 0 A_FireCustomMissile("PlasmaCaseSpawn",-5,0,-10,-15)
		22SR A 15 A_PlaySound("PLREL",CHAN_AUTO)
		22SR BCDEFGHIJ 1
		TNT1 A 0 A_PlaySound("PLREAD2",CHAN_AUTO)
		22SR KLMNOPQRSTUVX 1
	InsertBullets2:
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",40,"Ready")
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		Goto Ready
		TNT1 A 0 A_GiveInventory("PlasmaRifleAmmo",1)
		TNT1 A 0 A_Takeinventory("Cell",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets2
	AltFire:
		TNT1 A 0 A_JumpIf(waterlevel > 1,"Ready")
		Goto AltAmmoCheck
	ResumeAltFire:
		TNT1 A 0 A_GiveInventory("HasPlasmaWeapon",1)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,22)
		TNT1 A 0 A_FireCustomMissile("PlasmaFlame",0,0,0,-12,0,8)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("PlasFire",CHAN_WEAPON)
		PLSF AB 1 Bright
		PLSG A 1 A_SetPitch(random(1,-1) + pitch)
		TNT1 A 0 A_Refire
		TNT1 A 0 A_SpawnItemEx("PlasmaSmoke",30,0,40,0,0,2)
		PLSF C 1 offset(0,46)
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		PLSF C 1 offset(0,43)
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		PLSF D 1 offset(0,40)
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		PLSF D 1 offset(0,37)
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		PLSF E 1 offset(0,34)
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		PLSF E 1 offset(0,32)
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Goto Ready
	AmmoCheck:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_Takeinventory("PlasmaRifleAmmo",1,TIF_NOTAKEINFINITE)
		Goto ResumeFire
	NoReload:
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		Goto NoAmmo
		TNT1 A 0 A_Takeinventory("Cell",1,TIF_NOTAKEINFINITE)
		Goto ResumeFire
	AltAmmoCheck:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"AltNoReload")
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_Takeinventory("PlasmaRifleAmmo",1,TIF_NOTAKEINFINITE)
		Goto ResumeAltFire
	AltNoReload:
		TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		Goto NoAmmo
		TNT1 A 0 A_Takeinventory("Cell",1,TIF_NOTAKEINFINITE)
		Goto ResumeAltFire
	}
}

Actor 64PlasmaBall Replaces PlasmaBall
{
Scale 0.5
Radius 4
Height 4
Speed 50
Damage 10
Alpha 1
RenderStyle Add
Damagetype "Plasma"
SeeSound "weapons/plasmaf"
DeathSound "weapons/plasmax"
Obituary "$OB_MPPLASMARIFLE"
Decal "SmallerScorch"
Projectile
+RANDOMIZE
+MISSILE
+THRUSPECIES
	States
	{
	Spawn:
		TNT1 A 2
	Fly:
		PLSS A 1 Bright
		TNT1 A 0 A_SpawnItemEx("BlueFlareSmallProjectile")
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail1",(3 *momx)/-35.0,-(3 *momy)/-35.0,0+(3 *momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail2",(6 *momx)/-35.0,-(6 *momy)/-35.0,0+(6 *momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail3",(9 *momx)/-35.0,-(9 *momy)/-35.0,0+(9 *momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail4",(12*momx)/-35.0,-(12*momy)/-35.0,0+(12*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail5",(15*momx)/-35.0,-(15*momy)/-35.0,0+(15*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail6",(18*momx)/-35.0,-(18*momy)/-35.0,0+(18*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail7",(21*momx)/-35.0,-(21*momy)/-35.0,0+(21*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail8",(24*momx)/-35.0,-(24*momy)/-35.0,0+(24*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail9",(27*momx)/-35.0,-(27*momy)/-35.0,0+(27*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PlasmaBallTrail9",(30*momx)/-35.0,-(30*momy)/-35.0,0+(30*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterSmall")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterSmall")
		TNT1 A 0 A_CustomMissile("BluePlasmaFire",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 AAAA 1 A_SpawnItemEx("LensFlareTorchBlue")
		PLSE ABC 2 Bright A_CustomMissile("PlasmaSmoke",1,0,random(0,360),2,random(0,160))
		PLSE DEF 2 Bright
		Stop
	}
}

Actor 64ArachnotronPlasma : 64PlasmaBall Replaces ArachnotronPlasma
{
Speed 25
}

Actor PlasmaBallTrail1
{
Scale 0.25
Alpha 0.99
Renderstyle Add
+THRUACTORS
+CLIENTSIDEONLY
+NOBLOCKMAP
+NOGRAVITY
	States
	{
	Spawn:
		PLSS B 2 Bright
		Stop
	}
}

Actor PlasmaBallTrail2 : PlasmaBallTrail1
{
Alpha 0.8
	States
	{
	Spawn:
		PLSS A 2 Bright
		Stop
	}
}

Actor PlasmaBallTrail3 : PlasmaBallTrail1
{
Alpha 0.7
}

Actor PlasmaBallTrail4 : PlasmaBallTrail2
{
Alpha 0.6
}

Actor PlasmaBallTrail5 : PlasmaBallTrail2
{
Alpha 0.5
}

Actor PlasmaBallTrail6 : PlasmaBallTrail1
{
Alpha 0.4
}

Actor PlasmaBallTrail7 : PlasmaBallTrail2
{
Alpha 0.3
}

Actor PlasmaBallTrail8 : PlasmaBallTrail1
{
Alpha 0.2
}

Actor PlasmaBallTrail9 : PlasmaBallTrail2
{
Alpha 0.1
}

Actor PlasmaFlame
{
Damage(random(6,10))
Speed 40
Height 8
Radius 8
Gravity 1
RenderStyle Add
Alpha 1
DamageType "Plasma"
Decal "SmallerScorch"
DeathSound "world/lavsiz2"
+MISSILE
+NOBLOCKMAP
+NOTELEPORT
+DONTSPLASH
+FORCEXYBILLBOARD
+NOBLOOD
+NOBLOODDECALS
+BLOODLESSIMPACT
+NODAMAGETHRUST
-NOGRAVITY
-BLOODSPLATTER
+RIPPER
+NOBOSSRIP
	States
	{
	Spawn:
		PLSE AGA 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE G 1 Bright A_SetScale(2.0,3.0)
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		PLSE A 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE B 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE H 1 Bright A_SetScale(2.0,3.0)
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE B 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE C 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE I 1 Bright A_SetScale(2.0,3.0)
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE C 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE D 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE J 1 Bright A_SetScale(2.0,3.0)
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE D 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE E 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		TNT1 A 0 A_ChangeFlag("BOUNCEONWALLS",TRUE)
		TNT1 A 0 A_ChangeFlag("BOUNCEONFLOORS",TRUE)
		TNT1 A 0 A_ChangeFlag("ALLOWBOUNCEONACTORS",TRUE)
		TNT1 A 0 A_ChangeFlag("BOUNCEAUTOOFF",TRUE)
		TNT1 A 0 A_CustomMissile("ExplosionParticleHeavyBlue",0,0,random(-30,30),2,random(40,160))
		PLSE K 1 Bright A_SetScale(2.0,3.0)
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE E 1 Bright A_SetScale(1.0,2.0)
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE F 1 Bright
		TNT1 A 0 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_JumpIf(waterlevel > 0,"Death")
		PLSE L 1 Bright A_SetScale(2.0,3.0)
		TNT1 A 1 A_CustomMissile("BluePlasmaParticle",0,0,random(0,360),2,random(0,360))
		Stop
	Death:
		TNT1 A 0 A_CustomMissile("BluePlasmaFire",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("BlueplasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterSmall")
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterSmallMotherdemon")
		TNT1 A 0 A_SpawnItemEx("TinyBurningPieceBlue2",random(-25,25),random(-15,15))
		TNT1 A 0 A_SpawnItemEx("TinyBurningPieceBlue",random(-15,15),random(-15,15))
		TNT1 AA 0 A_CustomMissile("FlamethrowerFireParticlesBlue",6,0,random(0,360),2,random(10,90))
		TNT1 A 1
		Stop
	}
}